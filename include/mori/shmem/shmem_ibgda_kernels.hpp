// Copyright © Advanced Micro Devices, Inc. All rights reserved.
//
// MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.
#pragma once

#include <assert.h>

#include "mori/application/application_device_types.hpp"
#include "mori/core/core.hpp"
#include "mori/shmem/internal.hpp"

namespace mori {
namespace shmem {

#ifdef MORI_DEVICE_NIC_BNXT
#define DISPATCH_MLX5 0
#define DISPATCH_BNXT 1
#define DISPATCH_PSD 0
#define DISPATCH_JMRD 0
#elif defined(MORI_DEVICE_NIC_IONIC)
#define DISPATCH_MLX5 0
#define DISPATCH_BNXT 0
#define DISPATCH_PSD 1
#define DISPATCH_JMRD 0
#elif defined(MORI_DEVICE_NIC_JMRD)
#define DISPATCH_MLX5 0
#define DISPATCH_BNXT 0
#define DISPATCH_PSD 0
#define DISPATCH_JMRD 1
#else
#define DISPATCH_MLX5 1
#define DISPATCH_BNXT 0
#define DISPATCH_PSD 0
#define DISPATCH_JMRD 0
#endif

#define DISPATCH_PROVIDER_TYPE(func, ...)                             \
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();               \
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;             \
  core::ProviderType prvdType = ep[pe].GetProviderType();             \
  if (DISPATCH_MLX5 && prvdType == core::ProviderType::MLX5) {        \
    func<core::ProviderType::MLX5>(__VA_ARGS__);                      \
  } else if (DISPATCH_BNXT && prvdType == core::ProviderType::BNXT) { \
    func<core::ProviderType::BNXT>(__VA_ARGS__);                      \
  } else if (DISPATCH_PSD && prvdType == core::ProviderType::PSD) {   \
    func<core::ProviderType::PSD>(__VA_ARGS__);                       \
  } else if (DISPATCH_JMRD && prvdType == core::ProviderType::JMRD) { \
    func<core::ProviderType::JMRD>(__VA_ARGS__);                      \
  } else {                                                            \
    assert(false && "Unsupported or disabled provider type");         \
  }

#define DISPATCH_PROVIDER_TYPE_EP(ep, func, ...)                      \
  core::ProviderType prvdType = ep[pe].GetProviderType();             \
  if (DISPATCH_MLX5 && prvdType == core::ProviderType::MLX5) {        \
    func<core::ProviderType::MLX5>(__VA_ARGS__);                      \
  } else if (DISPATCH_BNXT && prvdType == core::ProviderType::BNXT) { \
    func<core::ProviderType::BNXT>(__VA_ARGS__);                      \
  } else if (DISPATCH_PSD && prvdType == core::ProviderType::PSD) {   \
    func<core::ProviderType::PSD>(__VA_ARGS__);                       \
  } else if (DISPATCH_JMRD && prvdType == core::ProviderType::JMRD) { \
    func<core::ProviderType::JMRD>(__VA_ARGS__);                      \
  } else {                                                            \
    assert(false && "Unsupported or disabled provider type");         \
  }

#define DISPATCH_PROVIDER_TYPE_COMPILE_TIME(func, ...) \
  do {                                                 \
    if constexpr (DISPATCH_BNXT == 1) {                \
      func<core::ProviderType::BNXT>(__VA_ARGS__);     \
    } else if constexpr (DISPATCH_PSD == 1) {          \
      func<core::ProviderType::PSD>(__VA_ARGS__);      \
    } else if constexpr (DISPATCH_JMRD == 1) {         \
      func<core::ProviderType::JMRD>(__VA_ARGS__);     \
    } else {                                           \
      func<core::ProviderType::MLX5>(__VA_ARGS__);     \
    }                                                  \
  } while (0)

#define DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_RETURN(func, type, ...) \
  [&]() {                                                                \
    if constexpr (DISPATCH_BNXT == 1) {                                  \
      return func<core::ProviderType::BNXT, type>(__VA_ARGS__);          \
    } else if constexpr (DISPATCH_PSD == 1) {                            \
      return func<core::ProviderType::PSD, type>(__VA_ARGS__);           \
    } else if constexpr (DISPATCH_JMRD == 1) {                           \
      return func<core::ProviderType::JMRD, type>(__VA_ARGS__);          \
    } else {                                                             \
      return func<core::ProviderType::MLX5, type>(__VA_ARGS__);          \
    }                                                                    \
  }()

#define DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(func, boolParam, ...) \
  do {                                                                      \
    if constexpr (DISPATCH_BNXT == 1) {                                     \
      func<core::ProviderType::BNXT, boolParam>(__VA_ARGS__);               \
    } else if constexpr (DISPATCH_PSD == 1) {                               \
      func<core::ProviderType::PSD, boolParam>(__VA_ARGS__);                \
    } else if constexpr (DISPATCH_JMRD == 1) {                              \
      func<core::ProviderType::JMRD, boolParam>(__VA_ARGS__);               \
    } else {                                                                \
      func<core::ProviderType::MLX5, boolParam>(__VA_ARGS__);               \
    }                                                                       \
  } while (0)

// Exclusive prefix sum of per-lane psnCnt over active lanes; warp-wide total via
// outTotal. Used for bnxt PSN accounting when lanes transfer different sizes.
inline __device__ uint32_t WarpActivePsnPrefix(uint32_t psnCnt, uint64_t activemask,
                                               uint32_t* outTotal) {
  const uint32_t myPhys = static_cast<uint32_t>(core::WarpLaneId());
  uint32_t excl = 0, total = 0;
  uint64_t m = activemask;
  while (m) {
    int l = __ffsll(static_cast<unsigned long long>(m)) - 1;
    uint32_t v = __shfl(psnCnt, l);
    total += v;
    if (static_cast<uint32_t>(l) < myPhys) excl += v;
    m &= m - 1;
  }
  *outTotal = total;
  return excl;
}

/* ---------------------------------------------------------------------------------------------- */
/*                                    VMM Heap Helper Functions                                   */
/* ---------------------------------------------------------------------------------------------- */
// Query VMM local key with chunk boundary calculation
inline __device__ void VmmQueryLocalKey(uintptr_t addr, size_t max_size, uint32_t& out_lkey,
                                        size_t& out_chunk_size) {
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  application::SymmMemObj* heapObj = globalGpuStates->heapObj;
  uintptr_t heapBase = globalGpuStates->heapBaseAddr;

  size_t offsetFromHeapBase = addr - heapBase;
  size_t chunkIdx = offsetFromHeapBase >> globalGpuStates->vmmChunkSizeShift;

  application::VMMChunkKey chunkKey = heapObj->vmmLkeyInfo[chunkIdx];

  out_lkey = chunkKey.key;
  size_t chunk_remaining = chunkKey.next_addr - addr;
  out_chunk_size = chunk_remaining < max_size ? chunk_remaining : max_size;
  MORI_PRINTF(
      "blockId %d, threadId %d VMM Heap: single transfer,chunkIdx: %zu, srcAddr: %p, lkey: %x, "
      "chunk_remaining: %zu, out_chunk_size: %zu\n",
      blockIdx.x, threadIdx.x, chunkIdx, addr, out_lkey, chunk_remaining, out_chunk_size);
}

// Query VMM remote address and key with chunk boundary calculation
inline __device__ void VmmQueryRemoteAddr(uintptr_t addr, int pe, size_t max_size,
                                          uintptr_t& out_raddr, uint32_t& out_rkey,
                                          size_t& out_chunk_size) {
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  application::SymmMemObj* heapObj = globalGpuStates->heapObj;
  uintptr_t heapBase = globalGpuStates->heapBaseAddr;

  size_t offsetFromHeapBase = addr - heapBase;
  size_t chunkIdx = offsetFromHeapBase >> globalGpuStates->vmmChunkSizeShift;

  out_raddr = heapObj->peerPtrs[pe] + offsetFromHeapBase;

  application::VMMChunkKey chunkKey = heapObj->vmmRkeyInfo[chunkIdx * heapObj->worldSize + pe];

  out_rkey = chunkKey.key;
  size_t chunk_remaining = chunkKey.next_addr - addr;
  out_chunk_size = chunk_remaining < max_size ? chunk_remaining : max_size;
  MORI_PRINTF(
      "blockId %d, threadId %d VMM Heap: single transfer,chunkIdx: %zu, dstAddr: %p, raddr: %lx, "
      "rkey: %x, chunk_remaining: %zu, out_chunk_size: %zu\n",
      blockIdx.x, threadIdx.x, chunkIdx, addr, out_raddr, out_rkey, chunk_remaining,
      out_chunk_size);
}

// Lookup VMM remote address and key (for small fixed-size transfers)
inline __device__ void VmmLookupRemote(uintptr_t addr, int pe, uintptr_t& out_raddr,
                                       uint32_t& out_rkey) {
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  application::SymmMemObj* heapObj = globalGpuStates->heapObj;
  uintptr_t heapBase = globalGpuStates->heapBaseAddr;

  size_t offsetFromHeapBase = addr - heapBase;
  size_t chunkIdx = offsetFromHeapBase >> globalGpuStates->vmmChunkSizeShift;

  out_raddr = heapObj->peerPtrs[pe] + offsetFromHeapBase;

  out_rkey = heapObj->vmmRkeyInfo[chunkIdx * heapObj->worldSize + pe].key;
}

/* ---------------------------------------------------------------------------------------------- */
/*                                         Synchronization                                        */
/* ---------------------------------------------------------------------------------------------- */
// Drain a collapsed (cqeNum==1) bnxt CQ and advance wq.doneIdx, reconstructing the
// completed count from CQE[0].con_indx (lock-free, mirrors Mlx5CollapsedCqDrain).
// DrainToLive=false drains to a dbTouchIdx snapshot; true waits for the live postIdx.
template <bool DrainToLive = false>
inline __device__ void BnxtCollapsedCqDrain(core::WorkQueueHandle& wq,
                                            core::CompletionQueueHandle& cq) {
  uint32_t exitTarget =
      DrainToLive ? __hip_atomic_load(&wq.postIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT)
                  : __hip_atomic_load(&wq.dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
  uint32_t cons = __hip_atomic_load(&wq.doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
  // Serial-order compare: these wrap at 2^32, a raw >= is wrong there.
  if (static_cast<int32_t>(cons - exitTarget) >= 0) return;

  const uint32_t mask = wq.sqWqeNum - 1;  // sqWqeNum is a power of two
  __threadfence();
  do {
    uint32_t consIdxIgnored = 0;  // collapsed CQ (cqeNum==1) always reads CQE[0]
    uint32_t wqeCounter = 0;
    int opcode =
        core::PollCq<core::ProviderType::BNXT>(cq.cqAddr, cq.cqeNum, &consIdxIgnored, &wqeCounter);
    if (opcode != BNXT_RE_REQ_ST_OK) {
      assert(false);
      return;
    }

    // con_indx is the consumed count mod sqWqeNum: rebuild the serial as a forward
    // delta from doneIdx, as Mlx5CollapsedCqDrain does with wqe_counter. Anchoring on
    // dbTouchIdx instead went a whole queue depth *backwards* whenever the CQE ran
    // ahead of it -- routine, since the doorbell is rung before dbTouchIdx is
    // published -- and as a raw word that pinned doneIdx for good. Issue #626.
    uint32_t dbTouch =
        __hip_atomic_load(&wq.dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t delta = (wqeCounter - cons) & mask;

    // Still capped at what was doorbelled: con_indx alone cannot tell a live
    // completion from a stale one. Refusing one costs nothing -- PollSingleCqe is
    // level-triggered for cqeNum == 1, so the loop re-reads the same value.
    if (delta != 0 && static_cast<int32_t>(delta) <= static_cast<int32_t>(dbTouch - cons)) {
      cons += delta;  // sole writer under pollCqLock, and delta > 0 is already an advance
      __hip_atomic_store(&wq.doneIdx, cons, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    }
    if constexpr (DrainToLive) {
      exitTarget = __hip_atomic_load(&wq.postIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    }
  } while (static_cast<int32_t>(cons - exitTarget) < 0);
  __threadfence();
}

template <bool DrainToLive = false>
inline __device__ void ShmemQuietThreadKernelSerialImpl(int pe, int qpId) {
  if (core::GetActiveLaneNum() != 0) return;
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle& wq = ep[epIndex].wqHandle;
  core::CompletionQueueHandle& cq = ep[epIndex].cqHandle;

  // One warp owns the drain; losers spin on cacheable reads and only CAS the lock
  // when it looks free. Recycle (DrainToLive=false) losers return; final-quiet
  // losers wait until doneIdx >= postIdx.
  while (true) {
    if constexpr (DrainToLive) {
      uint32_t done = __hip_atomic_load(&wq.doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t post = __hip_atomic_load(&wq.postIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      if (static_cast<int32_t>(done - post) >= 0) return;
    }
    if (__hip_atomic_load(&cq.pollCqLock, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT) == 0 &&
        core::AcquireLockOnce(&cq.pollCqLock)) {
      BnxtCollapsedCqDrain<DrainToLive>(wq, cq);
      core::ReleaseLock(&cq.pollCqLock);
      return;
    }
    if constexpr (!DrainToLive) return;
  }
}

inline __device__ void ShmemQuietThreadKernelPsdImpl(int pe, int qpId) {
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  const int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle& wqHandle = globalGpuStates->rdmaEndpoints[epIndex].wqHandle;
  core::CompletionQueueHandle& cqHandle = globalGpuStates->rdmaEndpoints[epIndex].cqHandle;

  const uint64_t activeMask = core::GetActiveLaneMask();
  const uint32_t myLogicalLaneId = core::GetActiveLaneNum(activeMask);
  const int myLaneId = core::WarpLaneId();

  constexpr uint32_t PENDING_WORK_MASK = 0x800000;  // Bit 23: sign bit for 24-bit counter
  constexpr uint32_t MSN_MASK = 0xFFFFFF;           // ionic reports a 24-bit MSN
  const uint32_t dbTouchedIdx = wqHandle.dbTouchIdx;
  constexpr uint32_t MAX_GREED = 10;
  constexpr uint32_t CQ_DOORBELL_GRACE = 100;  // IONIC_CQ_GRACE
  uint32_t wqeCounter;

  // Outer loop: retry lock acquisition until work is done
  while ((wqHandle.doneIdx - dbTouchedIdx) & PENDING_WORK_MASK) {
    if (!core::spin_lock_try_acquire_shared(&cqHandle.pollCqLock, activeMask)) {
      continue;  // Lock acquisition failed, retry
    }

    // Inner loop: process CQEs while holding the lock
    uint32_t greedRemaining = MAX_GREED;
    while ((wqHandle.doneIdx - dbTouchedIdx) & PENDING_WORK_MASK) {
      const uint64_t oldDoneIdx = wqHandle.doneIdx;

      const uint32_t curConsIdx = cqHandle.cq_consumer;
      uint32_t myCqPos = curConsIdx + myLogicalLaneId;

      // Poll CQE
      const int opcode = core::PollCq<core::ProviderType::PSD>(cqHandle.cqAddr, cqHandle.cqeNum,
                                                               &myCqPos, &wqeCounter);
      if (opcode > 0) {
        MORI_PRINTF("rank %d dest pe %d consIdx %d opcode %d\n", globalGpuStates->rank, pe, myCqPos,
                    opcode);
        assert(false);
      }
      asm volatile("" ::: "memory");

      const uint64_t successMask = __ballot(opcode == 0);
      const int highestLane = core::GetLastActiveLaneID(successMask);

      if (highestLane == -1) {
        continue;
      }

      if (myLaneId == highestLane) {
        // Widen the 24-bit MSN into doneIdx's 32-bit serial space: advance by the
        // masked forward delta instead of storing wqeCounter raw, else doneIdx ends
        // up in a different modulus than dbTouchIdx and the in-flight count goes bad
        // past 2^24. Cap it at what was doorbelled -- a WQE the NIC was never told
        // about cannot have completed -- reading dbTouchIdx live, since other warps
        // keep doorbelling while we poll. Read doneIdx once and write it once so
        // concurrent pollers off one CQE land on the same value; an RMW could reload
        // in between and apply delta twice, running doneIdx past the NIC.
        uint32_t doorbelled =
            __hip_atomic_load(&wqHandle.dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
        uint32_t cur = wqHandle.doneIdx;
        uint32_t delta = (wqeCounter - cur) & MSN_MASK;

        // Decide before consuming: the doorbell is rung before dbTouchIdx is
        // published, so a live MSN can be ahead of the cap however fresh the load,
        // and the color bit is keyed on cq_consumer -- moving it first would drop
        // that completion for good. Left in place, a later pass takes it.
        bool aheadOfDoorbell =
            delta != 0 && static_cast<int32_t>(delta) > static_cast<int32_t>(doorbelled - cur);
        if (!aheadOfDoorbell) {
          if (delta != 0) wqHandle.doneIdx = cur + delta;

          cqHandle.cq_consumer = myCqPos + 1;
          if (((cqHandle.cq_consumer - cqHandle.cq_dbpos) & (cqHandle.cqeNum - 1)) >=
              CQ_DOORBELL_GRACE) {
            cqHandle.cq_dbpos = cqHandle.cq_consumer;
            core::UpdateCqDbrRecord<core::ProviderType::PSD>(cqHandle, myCqPos + 1);
          }
        }
      }

      if (!((wqHandle.doneIdx - dbTouchedIdx) & PENDING_WORK_MASK)) {
        if (wqHandle.doneIdx == oldDoneIdx) {
          break;
        }
        if (greedRemaining == 0) {
          break;
        }
        --greedRemaining;
      }
    }

    core::spin_lock_release_shared(&cqHandle.pollCqLock, activeMask);
    break;  // Work done, exit outer loop
  }
}

// Collapsed CQ (cc=1): NIC keeps the latest completion in CQE[0]; rebuild doneIdx from
// wqe_counter. Caller must hold pollCqLock (see ShmemQuietThreadKernelMlnxImpl).
template <bool DrainToLive = false>
inline __device__ void Mlx5CollapsedCqDrain(core::WorkQueueHandle& wq,
                                            core::CompletionQueueHandle& cq) {
  uint32_t exitTarget =
      DrainToLive ? __hip_atomic_load(&wq.postIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT)
                  : __hip_atomic_load(&wq.dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
  uint32_t cons = __hip_atomic_load(&wq.doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
  // Serial-order compare: these wrap at 2^32, a raw >= is wrong there.
  if (static_cast<int32_t>(cons - exitTarget) >= 0) return;

  volatile core::Mlx5Cqe64* cqe = reinterpret_cast<volatile core::Mlx5Cqe64*>(cq.cqAddr);
  __threadfence();

  do {
    uint32_t prevCons = cons;
    uint16_t wqeCounter = BE16TOH(cqe->wqe_counter);
    uint8_t opcode =
        (reinterpret_cast<volatile uint8_t*>(cq.cqAddr)[sizeof(core::Mlx5Cqe64) - 1]) >> 4;
    if (opcode == core::MORI_MLX5_CQE_REQ_ERR || opcode == core::MORI_MLX5_CQE_RESP_ERR) {
      auto error = core::Mlx5HandleErrorCqe(reinterpret_cast<core::Mlx5ErrCqe*>(cq.cqAddr));
      MORI_PRINTF("(%s:%d) collapsed CQE error: %s\n", __FILE__, __LINE__,
                  core::WcStatusString(error));
      assert(false);
      return;
    }

    uint16_t comp16 = static_cast<uint16_t>(wqeCounter + 1);
    uint16_t delta = static_cast<uint16_t>(comp16 - static_cast<uint16_t>(cons));
    // Clamp against dbTouchIdx even when draining to postIdx. A completion cannot
    // exist for a WQE that was never doorbelled, and postIdx counts reservations
    // whose WQEs are not in the SQ yet, so it runs tens of thousands ahead here.
    // Bounding by it accepts a wqe_counter that does not belong to the live window
    // -- the 16-bit value then wraps into a plausible-looking delta -- and pushes
    // doneIdx past WQEs the NIC has not fetched. The recycle gate hands those still
    // live SQ slots out for reuse and the overwritten writes are silently dropped.
    uint32_t doorbelled =
        __hip_atomic_load(&wq.dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    // Caller holds pollCqLock and nothing else writes doneIdx on this path, so a
    // plain store replaces the atomic max -- delta > 0 already makes it an advance
    // -- and the reload after it, which could only return what we just stored.
    if (delta != 0 && static_cast<int32_t>(delta) <= static_cast<int32_t>(doorbelled - cons)) {
      cons += delta;
      __hip_atomic_store(&wq.doneIdx, cons, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    }
    if (cons == prevCons) break;
    if constexpr (DrainToLive) {
      exitTarget = __hip_atomic_load(&wq.postIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    }
  } while (static_cast<int32_t>(cons - exitTarget) < 0);
  __threadfence();
}

// Collapsed CQ (cc=1): lane 0 drains under pollCqLock; retry final quiet when CQE[0]
// makes no progress.
template <bool DrainToLive = false>
inline __device__ void ShmemQuietThreadKernelMlnxImpl(int pe, int qpId) {
  if (core::GetActiveLaneNum() != 0) return;
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle& wq = ep[epIndex].wqHandle;
  core::CompletionQueueHandle& cq = ep[epIndex].cqHandle;

  while (true) {
    if constexpr (DrainToLive) {
      uint32_t done = __hip_atomic_load(&wq.doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t post = __hip_atomic_load(&wq.postIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      if (static_cast<int32_t>(done - post) >= 0) return;
    }
    if (__hip_atomic_load(&cq.pollCqLock, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT) == 0 &&
        core::AcquireLockOnce(&cq.pollCqLock)) {
      Mlx5CollapsedCqDrain<DrainToLive>(wq, cq);
      core::ReleaseLock(&cq.pollCqLock);
      if constexpr (DrainToLive) {
        continue;
      }
      return;
    }
    if constexpr (!DrainToLive) return;
  }
}

template <typename T>
__device__ __forceinline__ bool is_geq(T v0, T v1) {
  int gap = v0 - v1;

  if (gap >= 0)
    return true;
  else
    return false;
}

// Advance a modulo-2^32 sequence counter without ever moving it backwards.
// A candidate is newer only when it is less than half the sequence space ahead
// of the observed value.  The SQ outstanding window is far below 2^31, so this
// is unambiguous even when the counter wraps.
inline __device__ void atomic_advance_mod32(uint32_t* counter, uint32_t target) {
  uint32_t observed = __hip_atomic_load(counter, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
  while (true) {
    int32_t forward = target - observed;
    if (forward <= 0) return;

    uint32_t expected = observed;
    if (__hip_atomic_compare_exchange_strong(counter, &expected, target, __ATOMIC_RELAXED,
                                             __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT)) {
      return;
    }
    // A concurrent updater changed the value; retry against the returned value.
    observed = expected;
  }
}

template <bool DrainToLive = false>
inline __device__ void ShmemQuietThreadKernelJmrdImpl(int pe, int qpId) {
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle& wq = ep[epIndex].wqHandle;
  core::CompletionQueueHandle& cq = ep[epIndex].cqHandle;

  uint64_t activemask = core::GetActiveLaneMask();
  uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
  uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
  bool is_leader{my_logical_lane_id == num_active_lanes - 1};

  if (!is_leader) {
    return;
  }
  uint32_t exitTarget;
  if (DrainToLive) {
    while (true) {
      uint32_t touched =
          __hip_atomic_load(&wq.dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      exitTarget = __hip_atomic_load(&wq.postIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      if (touched >= exitTarget) break;
    }
  } else {
    exitTarget = __hip_atomic_load(&wq.dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
  }
  uint32_t cons = __hip_atomic_load(&wq.doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);

  if (is_geq(cons, exitTarget)) return;

  uint32_t needConsIdx =
      __hip_atomic_load(&cq.needConsIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);

  uint32_t cqeIdx = (needConsIdx - 1) & (cq.cqeNum - 1);
  volatile core::jm_cqe* cqe =
      reinterpret_cast<core::jm_cqe*>((char*)cq.cqAddr + cqeIdx * sizeof(core::jm_cqe));
  __threadfence();
  while (true) {
    cons = __hip_atomic_load(&wq.doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    if (is_geq(cons, exitTarget)) return;
    if (!!cqe->owner != !!((needConsIdx - 1) & cq.cqeNum)) {
      break;
    }
  }

  if (cqe->status != 0) {
    MORI_PRINTF("(%s:%d) CQE error: %s substats:%d\n", __FILE__, __LINE__,
                core::WcStatusString(static_cast<core::WcStatus>(cqe->status)), cqe->sub_type);
    assert(false);
    return;
  }

  atomic_advance_mod32(reinterpret_cast<uint32_t*>(cq.dbrRecAddr), needConsIdx);
  atomic_advance_mod32(&wq.doneIdx, exitTarget);

  return;
}

// DrainToLive=false (recycle gate, per-QP): snapshot drain. The caller holds its
// own un-doorbelled reservation, so a live (postIdx) drain would self-deadlock.
// true (final per-pe quiet): wait for the live postIdx -- every reserved WQE done.
template <core::ProviderType PrvdType, bool DrainToLive = false>
inline __device__ void ShmemQuietThreadKernelImpl(int pe, int qpId) {
  if constexpr (PrvdType == core::ProviderType::BNXT) {
    ShmemQuietThreadKernelSerialImpl<DrainToLive>(pe, qpId);
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    ShmemQuietThreadKernelPsdImpl(pe, qpId);
  } else if constexpr (PrvdType == core::ProviderType::MLX5) {
    ShmemQuietThreadKernelMlnxImpl<DrainToLive>(pe, qpId);
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    ShmemQuietThreadKernelJmrdImpl<DrainToLive>(pe, qpId);
  } else {
    static_assert(false);
  }
}

// A peer can be RDMA-typed yet have no QP in this Context: under
// MORI_ENABLE_RAIL_ONLY only same-rail peers are connected, and the rest keep the
// empty-stub slots that also stand in for non-RDMA peers. A stub's qpn is 0,
// which ibverbs never hands out (QP0 is reserved), so it is an exact
// "connected" test. Always true when rail-only is off.
__device__ __forceinline__ bool ShmemPeerHasQp(int pe) {
  GpuStates* s = GetGlobalGpuStatesPtr();
  return s->rdmaEndpoints[pe * s->numQpPerPe].qpn != 0;
}

template <>
inline __device__ void ShmemQuietThreadKernel<application::TransportType::RDMA>() {
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  int rank = globalGpuStates->rank;
  int worldSize = globalGpuStates->worldSize;
  for (int peId = 0; peId < worldSize; peId++) {
    if (peId != rank && globalGpuStates->transportTypes[peId] == application::TransportType::RDMA &&
        ShmemPeerHasQp(peId)) {
      for (int qpId = 0; qpId < globalGpuStates->numQpPerPe; qpId++) {
        // Real completion wait (DrainToLive=true), like the per-PE / per-QP overloads.
        if constexpr (DISPATCH_BNXT == 1) {
          ShmemQuietThreadKernelImpl<core::ProviderType::BNXT, true>(peId, qpId);
        } else if constexpr (DISPATCH_PSD == 1) {
          ShmemQuietThreadKernelImpl<core::ProviderType::PSD, true>(peId, qpId);
        } else if constexpr (DISPATCH_JMRD == 1) {
          ShmemQuietThreadKernelImpl<core::ProviderType::JMRD, true>(peId, qpId);
        } else {
          ShmemQuietThreadKernelImpl<core::ProviderType::MLX5, true>(peId, qpId);
        }
      }
    }
  }
}

// Quiet all QPs to `pe`: returns only once every reserved WQE has completed (the
// live drain, DrainToLive=true -> waits for postIdx). The per-QP recycle gate
// instead uses ShmemQuietThreadKernelImpl with the default snapshot drain, which
// must stay snapshot there to avoid self-deadlock.
template <>
inline __device__ void ShmemQuietThreadKernel<application::TransportType::RDMA>(int pe) {
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  int rank = globalGpuStates->rank;
  if (pe == rank) return;
  if (globalGpuStates->transportTypes[pe] != application::TransportType::RDMA) return;
  if (!ShmemPeerHasQp(pe)) return;
  for (int qpId = 0; qpId < globalGpuStates->numQpPerPe; qpId++) {
    if constexpr (DISPATCH_BNXT == 1) {
      ShmemQuietThreadKernelImpl<core::ProviderType::BNXT, true>(pe, qpId);
    } else if constexpr (DISPATCH_PSD == 1) {
      ShmemQuietThreadKernelImpl<core::ProviderType::PSD, true>(pe, qpId);
    } else if constexpr (DISPATCH_JMRD == 1) {
      ShmemQuietThreadKernelImpl<core::ProviderType::JMRD, true>(pe, qpId);
    } else {
      ShmemQuietThreadKernelImpl<core::ProviderType::MLX5, true>(pe, qpId);
    }
  }
}

template <>
inline __device__ void ShmemQuietThreadKernel<application::TransportType::RDMA>(int pe, int qpId) {
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int rank = globalGpuStates->rank;
  if (pe == rank) return;
  if (globalGpuStates->transportTypes[pe] != application::TransportType::RDMA) return;
  if (!ShmemPeerHasQp(pe)) return;
  // Real completion wait (DrainToLive=true): the caller's own WQEs must be done on
  // return (e.g. a GET reads its local dest right after). Snapshot drain is only
  // for the recycle gate, which calls ShmemQuietThreadKernelImpl directly.
  if constexpr (DISPATCH_BNXT == 1) {
    ShmemQuietThreadKernelImpl<core::ProviderType::BNXT, true>(pe, qpId);
  } else if constexpr (DISPATCH_PSD == 1) {
    ShmemQuietThreadKernelImpl<core::ProviderType::PSD, true>(pe, qpId);
  } else if constexpr (DISPATCH_JMRD == 1) {
    ShmemQuietThreadKernelImpl<core::ProviderType::JMRD, true>(pe, qpId);
  } else {
    ShmemQuietThreadKernelImpl<core::ProviderType::MLX5, true>(pe, qpId);
  }
}

/* ---------------------------------------------------------------------------------------------- */
/*                                         Point-to-Point                                         */
/* ---------------------------------------------------------------------------------------------- */
template <core::ProviderType PrvdType>
inline __device__ void ShmemPutMemNbiThreadKernelImpl(const application::SymmMemObjPtr dest,
                                                      size_t destOffset,
                                                      const application::SymmMemObjPtr source,
                                                      size_t sourceOffset, size_t bytes, int pe,
                                                      int qpId) {
  if (bytes == 0) return;

  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle* wq = &ep[epIndex].wqHandle;
  core::CompletionQueueHandle* cq = &ep[epIndex].cqHandle;
  uint32_t qpn = ep[epIndex].qpn;

  bool needsChunking = globalGpuStates->useVMMHeap;
  size_t currentOffset = 0;
  size_t remaining = bytes;

  while (true) {
    // Check if current thread still has data to transfer
    bool has_remaining = (remaining > 0);

    // Synchronize within warp: get mask of threads that still have work
    uint64_t activemask = __ballot(has_remaining);
    if (activemask == 0) {
      break;  // All threads in warp are done
    }

    // Recalculate active lane info for threads with remaining data
    uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
    uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
    bool is_leader{my_logical_lane_id == num_active_lanes - 1};
    const uint64_t leader_phys_lane_id = core::GetLastActiveLaneID(activemask);

    // Inactive threads skip actual work but stay synchronized
    if (!has_remaining) {
      continue;
    }

    // Get RDMA keys and addresses based on mode
    uint32_t lkey, rkey;
    uintptr_t srcAddr, raddr;
    size_t transfer_size;

    if (!needsChunking) {
      // Isolation or Static Heap - direct access
      // Keys are uniform, transfer entire remaining bytes in one shot
      lkey = source->lkey;
      srcAddr = reinterpret_cast<uintptr_t>(source->localPtr) + sourceOffset + currentOffset;
      raddr = dest->peerPtrs[pe] + destOffset + currentOffset;
      rkey = dest->peerRkeys[pe];
      transfer_size = remaining;
    } else {
      // Slow path: VMM Heap - query keys for current chunk
      srcAddr = reinterpret_cast<uintptr_t>(source->localPtr) + sourceOffset + currentOffset;
      size_t src_chunk_size;
      VmmQueryLocalKey(srcAddr, remaining, lkey, src_chunk_size);

      uintptr_t dstAddr = reinterpret_cast<uintptr_t>(dest->localPtr) + destOffset + currentOffset;
      size_t dst_chunk_size;
      VmmQueryRemoteAddr(dstAddr, pe, remaining, raddr, rkey, dst_chunk_size);

      transfer_size = src_chunk_size < dst_chunk_size ? src_chunk_size : dst_chunk_size;
      // MORI_PRINTF("blockId %d, threadId %d VMM Heap: single transfer,srcAddr: %p, dstAddr: %p,
      // lkey: %x, raddr: %lx, rkey: %x, transfer_size: %zu\n", blockIdx.x, threadIdx.x, srcAddr,
      // dstAddr, lkey, raddr, rkey, transfer_size);
    }
    MORI_PRINTF("blockIdx.x=%d, threadIdx.x=%d, remaining=%zu, transfer_size=%zu\n", blockIdx.x,
                threadIdx.x, remaining, transfer_size);
    // Post RDMA write (unified code for both fast and slow paths)
    uint32_t warp_sq_counter{0};
    uint32_t warp_msntbl_counter{0}, warp_psn_counter{0}, warp_rsn_counter{0};
    uint32_t my_sq_counter{0}, my_msntbl_counter{0}, my_psn_counter{0}, my_rsn_counter{0};
    uint32_t psnCnt = 0;
    uint32_t warp_total_psn = 0, my_psn_excl = 0, warp_total_rsn = 0, my_rsn_excl = 0;

    if constexpr (PrvdType == core::ProviderType::BNXT) {
      psnCnt = (transfer_size + wq->mtuSize - 1) / wq->mtuSize;
      my_psn_excl = WarpActivePsnPrefix(psnCnt, activemask, &warp_total_psn);
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      psnCnt = (transfer_size + wq->mtuSize - 1) / wq->mtuSize;
      my_psn_excl = WarpActivePsnPrefix(psnCnt, activemask, &warp_total_psn);
    }

    if (is_leader) {
      if constexpr (PrvdType == core::ProviderType::MLX5) {
        warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                                 __HIP_MEMORY_SCOPE_AGENT);
      } else if constexpr (PrvdType == core::ProviderType::JMRD) {
        warp_sq_counter = core::atomic_add_packed_rsn_and_psn(*wq, num_active_lanes, warp_total_psn,
                                                              &warp_psn_counter);
      } else if constexpr (PrvdType == core::ProviderType::BNXT) {
        core::atomic_add_packed_msn_and_psn(&wq->msnPack, num_active_lanes, warp_total_psn,
                                            &warp_msntbl_counter, &warp_psn_counter);
        warp_sq_counter = warp_msntbl_counter;
        core::AtomicMaxSerial(&wq->postIdx, warp_sq_counter + num_active_lanes);
      } else if constexpr (PrvdType == core::ProviderType::PSD) {
        warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                                 __HIP_MEMORY_SCOPE_AGENT);
      } else {
        static_assert(false);
      }
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    if constexpr (PrvdType == core::ProviderType::MLX5) {
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
      warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
      my_psn_counter = warp_psn_counter + my_psn_excl;
    } else if constexpr (PrvdType == core::ProviderType::BNXT) {
      warp_msntbl_counter = __shfl(warp_msntbl_counter, leader_phys_lane_id);
      warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
      my_msntbl_counter = warp_msntbl_counter + my_logical_lane_id;
      my_psn_counter = warp_psn_counter + my_psn_excl;
    } else if constexpr (PrvdType == core::ProviderType::PSD) {
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
    } else {
      static_assert(false);
    }

    while (true) {
      uint32_t db_touched =
          __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t db_done =
          __hip_atomic_load(&wq->doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t num_active_sq_entries = db_touched - db_done;
      uint32_t num_free_entries = wq->sqWqeNum - num_active_sq_entries;
      uint32_t num_entries_until_warp_last_entry = warp_sq_counter + num_active_lanes - db_touched;
      if (num_free_entries > num_entries_until_warp_last_entry) {
        break;
      }
      ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
    }

    uint64_t dbr_val;
    if constexpr (PrvdType == core::ProviderType::MLX5) {
      dbr_val =
          core::PostWrite<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader,
                                    qpn, srcAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      dbr_val =
          core::PostWrite<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter,
                                    is_leader, qpn, srcAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::BNXT) {
      dbr_val =
          core::PostWrite<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter,
                                    is_leader, qpn, srcAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::PSD) {
      dbr_val =
          core::PostWrite<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader,
                                    qpn, srcAddr, lkey, raddr, rkey, transfer_size);
    } else {
      static_assert(false);
    }
    __threadfence_system();
    if (is_leader) {
      uint64_t db_touched{0};
      do {
        db_touched = __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      } while (db_touched != warp_sq_counter);

      core::UpdateSendDbrRecord<PrvdType>(wq->dbrRecAddr, warp_sq_counter + num_active_lanes);
      __threadfence_system();
      core::RingDoorbell<PrvdType>(wq->dbrAddr, dbr_val);
      __threadfence_system();

      __hip_atomic_fetch_add(&cq->needConsIdx, 1, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      __hip_atomic_store(&wq->dbTouchIdx, warp_sq_counter + num_active_lanes, __ATOMIC_RELAXED,
                         __HIP_MEMORY_SCOPE_AGENT);
    }
    __threadfence_system();

    // Move to next chunk (for VMM heap) or exit loop (for Isolation/Static heap)
    currentOffset += transfer_size;
    remaining -= transfer_size;
  }
}

template <>
inline __device__ void ShmemPutMemNbiThreadKernel<application::TransportType::RDMA>(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes, int pe, int qpId) {
  assert(ShmemPeerHasQp(pe) && "RDMA put to a peer with no QP (rail-only stub?)");
  bool need_turn{true};
  uint64_t turns = __ballot(need_turn);
  while (turns) {
    uint8_t lane = __ffsll((unsigned long long)turns) - 1;
    int pe_turn = __shfl(pe, lane);
    if (pe_turn == pe) {
      DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemPutMemNbiThreadKernelImpl, dest, destOffset, source,
                                          sourceOffset, bytes, pe, qpId);
      need_turn = false;
    }
    turns = __ballot(need_turn);
  }
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemPutMemNbiWarpKernelImpl(const application::SymmMemObjPtr dest,
                                                    size_t destOffset,
                                                    const application::SymmMemObjPtr source,
                                                    size_t sourceOffset, size_t bytes, int pe,
                                                    int qpId) {
  int laneId = threadIdx.x & (warpSize - 1);
  if (laneId == 0) {
    ShmemPutMemNbiThreadKernelImpl<PrvdType>(dest, destOffset, source, sourceOffset, bytes, pe,
                                             qpId);
  }
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemPutMemNbiBlockKernelImpl(const application::SymmMemObjPtr dest,
                                                     size_t destOffset,
                                                     const application::SymmMemObjPtr source,
                                                     size_t sourceOffset, size_t bytes, int pe,
                                                     int qpId) {
  int threadId = core::FlatBlockThreadId();
  if (threadId == 0) {
    ShmemPutMemNbiThreadKernelImpl<PrvdType>(dest, destOffset, source, sourceOffset, bytes, pe,
                                             qpId);
  }
}

template <>
inline __device__ void ShmemPutMemNbiWarpKernel<application::TransportType::RDMA>(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemPutMemNbiWarpKernelImpl, dest, destOffset, source,
                                      sourceOffset, bytes, pe, qpId);
}

template <>
inline __device__ void ShmemPutMemNbiBlockKernel<application::TransportType::RDMA>(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemPutMemNbiBlockKernelImpl, dest, destOffset, source,
                                      sourceOffset, bytes, pe, qpId);
}

// TODO: deal with bytes count limit
// TODO: put size api only support 1,2,4,8,16 in nvshmem, should we do that?
template <core::ProviderType PrvdType>
inline __device__ void ShmemPutSizeImmNbiThreadKernelImpl(const application::SymmMemObjPtr dest,
                                                          size_t destOffset, void* val,
                                                          size_t bytes, int pe, int qpId) {
  if (bytes == 0) return;
  // assert(destOffset + bytes <= dest->size);

  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();

  // Get correct rkey for VMM heap or use direct rkey for Isolation/Static Heap
  uintptr_t raddr;
  uint32_t rkey;
  if (globalGpuStates->useVMMHeap) {
    // VMM Heap: data is small (≤16 bytes), won't cross chunk boundary
    uintptr_t dstAddr = reinterpret_cast<uintptr_t>(dest->localPtr) + destOffset;
    VmmLookupRemote(dstAddr, pe, raddr, rkey);
  } else {
    // Isolation or Static Heap: direct access
    raddr = dest->peerPtrs[pe] + destOffset;
    rkey = dest->peerRkeys[pe];
  }
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle* wq = &ep[epIndex].wqHandle;
  core::CompletionQueueHandle* cq = &ep[epIndex].cqHandle;
  uint32_t qpn = ep[epIndex].qpn;

  uint64_t activemask = core::GetActiveLaneMask();
  uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
  uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
  bool is_leader{my_logical_lane_id == num_active_lanes - 1};
  const uint64_t leader_phys_lane_id = core::GetLastActiveLaneID(activemask);
  uint32_t warp_sq_counter{0};
  uint32_t warp_msntbl_counter{0}, warp_psn_counter{0};
  uint32_t my_sq_counter{0}, my_msntbl_counter{0}, my_psn_counter{0};

  if constexpr (PrvdType == core::ProviderType::MLX5) {
    if (is_leader) {
      warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                               __HIP_MEMORY_SCOPE_AGENT);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    if (is_leader) {
      warp_sq_counter = core::atomic_add_packed_rsn_and_psn(*wq, num_active_lanes, num_active_lanes,
                                                            &warp_psn_counter);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
    my_psn_counter = warp_psn_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::BNXT) {
    if (is_leader) {
      core::atomic_add_packed_msn_and_psn(&wq->msnPack, num_active_lanes, num_active_lanes,
                                          &warp_msntbl_counter, &warp_psn_counter);
      warp_sq_counter = warp_msntbl_counter;
      core::AtomicMaxSerial(&wq->postIdx, warp_sq_counter + num_active_lanes);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    warp_msntbl_counter = __shfl(warp_msntbl_counter, leader_phys_lane_id);
    warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
    my_msntbl_counter = warp_msntbl_counter + my_logical_lane_id;
    my_psn_counter = warp_psn_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    if (is_leader) {
      warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                               __HIP_MEMORY_SCOPE_AGENT);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
  } else {
    static_assert(false);
  }

  while (true) {
    uint32_t db_touched =
        __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t db_done = __hip_atomic_load(&wq->doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t num_active_sq_entries = db_touched - db_done;
    uint32_t num_free_entries = wq->sqWqeNum - num_active_sq_entries;
    uint32_t num_entries_until_warp_last_entry = warp_sq_counter + num_active_lanes - db_touched;
    if (num_free_entries > num_entries_until_warp_last_entry) {
      break;
    }
    ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
  }

  uint64_t dbr_val;
  if constexpr (PrvdType == core::ProviderType::MLX5) {
    dbr_val = core::PostWriteInline<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter,
                                              is_leader, qpn, val, raddr, rkey, bytes);
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    dbr_val = core::PostWriteInline<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter,
                                              is_leader, qpn, val, raddr, rkey, bytes);
  } else if constexpr (PrvdType == core::ProviderType::BNXT) {
    dbr_val = core::PostWriteInline<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter,
                                              is_leader, qpn, val, raddr, rkey, bytes);
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    dbr_val = core::PostWriteInline<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter,
                                              is_leader, qpn, val, raddr, rkey, bytes);
  } else {
    static_assert(false);
  }
  __threadfence_system();
  if (is_leader) {
    uint64_t db_touched = 0;
    do {
      db_touched = __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    } while (db_touched != warp_sq_counter);

    core::UpdateSendDbrRecord<PrvdType>(wq->dbrRecAddr, warp_sq_counter + num_active_lanes);
    __threadfence_system();
    core::RingDoorbell<PrvdType>(wq->dbrAddr, dbr_val);
    __threadfence_system();

    __hip_atomic_fetch_add(&cq->needConsIdx, 1, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    __hip_atomic_store(&wq->dbTouchIdx, warp_sq_counter + num_active_lanes, __ATOMIC_RELAXED,
                       __HIP_MEMORY_SCOPE_AGENT);
  }
  __threadfence_system();
}

template <>
inline __device__ void ShmemPutSizeImmNbiThreadKernel<application::TransportType::RDMA>(
    const application::SymmMemObjPtr dest, size_t destOffset, void* val, size_t bytes, int pe,
    int qpId) {
  bool need_turn{true};
  uint64_t turns = __ballot(need_turn);
  while (turns) {
    uint8_t lane = __ffsll((unsigned long long)turns) - 1;
    int pe_turn = __shfl(pe, lane);
    if (pe_turn == pe) {
      DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemPutSizeImmNbiThreadKernelImpl, dest, destOffset, val,
                                          bytes, pe, qpId);
      need_turn = false;
    }
    turns = __ballot(need_turn);
  }
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemPutSizeImmNbiWarpKernelImpl(const application::SymmMemObjPtr dest,
                                                        size_t destOffset, void* val, size_t bytes,
                                                        int pe, int qpId) {
  int laneId = threadIdx.x & (warpSize - 1);
  if (laneId == 0) {
    ShmemPutSizeImmNbiThreadKernelImpl<PrvdType>(dest, destOffset, val, bytes, pe, qpId);
  }
}

template <>
inline __device__ void ShmemPutSizeImmNbiWarpKernel<application::TransportType::RDMA>(
    const application::SymmMemObjPtr dest, size_t destOffset, void* val, size_t bytes, int pe,
    int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemPutSizeImmNbiWarpKernelImpl, dest, destOffset, val,
                                      bytes, pe, qpId);
}

template <core::ProviderType PrvdType, bool onlyOneSignal = true>
inline __device__ void ShmemPutMemNbiSignalThreadKernelImpl(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes,
    const application::SymmMemObjPtr signalDest, size_t signalDestOffset, uint64_t signalValue,
    core::atomicType signalOp, int pe, int qpId) {
  if (bytes == 0) return;
  // assert(sourceOffset + bytes <= source->size && destOffset + bytes <= dest->size);

  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle* wq = &ep[epIndex].wqHandle;
  core::CompletionQueueHandle* cq = &ep[epIndex].cqHandle;
  uint32_t qpn = ep[epIndex].qpn;

  bool needsChunking = globalGpuStates->useVMMHeap;
  size_t currentOffset = 0;
  size_t remaining = bytes;

  while (true) {
    // Check if current thread still has data to transfer
    bool has_remaining = (remaining > 0);

    // Synchronize within warp: get mask of threads that still have work
    uint64_t activemask = __ballot(has_remaining);
    if (activemask == 0) {
      break;  // All threads in warp are done
    }

    // Recalculate active lane info for threads with remaining data
    uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
    uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
    bool is_leader{my_logical_lane_id == num_active_lanes - 1};
    const uint64_t leader_phys_lane_id = core::GetLastActiveLaneID(activemask);

    // Inactive threads skip actual work but stay synchronized
    if (!has_remaining) {
      continue;
    }

    // Get RDMA keys and addresses for current chunk
    uint32_t lkey, rkey;
    uintptr_t laddr, raddr;
    size_t transfer_size;

    if (!needsChunking) {
      // Fast path: Isolation or Static Heap
      lkey = source->lkey;
      laddr = reinterpret_cast<uintptr_t>(source->localPtr) + sourceOffset + currentOffset;
      raddr = dest->peerPtrs[pe] + destOffset + currentOffset;
      rkey = dest->peerRkeys[pe];
      transfer_size = remaining;
    } else {
      // Slow path: VMM Heap - query keys for current chunk
      uintptr_t srcAddr =
          reinterpret_cast<uintptr_t>(source->localPtr) + sourceOffset + currentOffset;
      size_t src_chunk_size;
      VmmQueryLocalKey(srcAddr, remaining, lkey, src_chunk_size);
      laddr = srcAddr;

      uintptr_t dstAddr = reinterpret_cast<uintptr_t>(dest->localPtr) + destOffset + currentOffset;
      size_t dst_chunk_size;
      VmmQueryRemoteAddr(dstAddr, pe, remaining, raddr, rkey, dst_chunk_size);

      transfer_size = src_chunk_size < dst_chunk_size ? src_chunk_size : dst_chunk_size;
    }

    // Each thread checks if this is its last chunk
    bool my_is_last_chunk = (transfer_size == remaining);

    // Synchronize: only send signal if ALL active threads are on their last chunk
    // This ensures warp-uniform decision on num_wqes
    uint64_t all_last_mask = __ballot(my_is_last_chunk);
    bool isLastChunk = (all_last_mask == activemask);

    uint32_t warp_sq_counter{0};
    uint32_t warp_msntbl_counter{0}, warp_psn_counter{0}, warp_rsn_counter{0};
    uint32_t my_sq_counter{0}, my_msntbl_counter{0}, my_psn_counter{0}, my_rsn_counter{0};
    uint32_t psnCnt = 0;
    uint32_t warp_total_psn = 0, my_psn_excl = 0, warp_total_rsn = 0, my_rsn_excl = 0;
    // For last chunk: add 1 WQE for signal; for other chunks: just put
    uint32_t num_wqes = isLastChunk ? (onlyOneSignal ? num_active_lanes + 1 : num_active_lanes * 2)
                                    : num_active_lanes;

    if constexpr (PrvdType == core::ProviderType::BNXT) {
      psnCnt = (transfer_size + wq->mtuSize - 1) / wq->mtuSize;
      // Per-lane PSN unit includes this lane's signal WQE when each lane signals.
      uint32_t psnUnit = (isLastChunk && !onlyOneSignal) ? (psnCnt + 1) : psnCnt;
      my_psn_excl = WarpActivePsnPrefix(psnUnit, activemask, &warp_total_psn);
      if (isLastChunk && onlyOneSignal) warp_total_psn += 1;
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      psnCnt = (transfer_size + wq->mtuSize - 1) / wq->mtuSize;
      // Per-lane PSN unit includes this lane's signal WQE when each lane signals.
      uint32_t psnUnit = (isLastChunk && !onlyOneSignal) ? (psnCnt + 1) : psnCnt;
      my_psn_excl = WarpActivePsnPrefix(psnUnit, activemask, &warp_total_psn);
      if (isLastChunk && onlyOneSignal) {
        warp_total_psn += 1;
        if (signalOp == core::atomicType::AMO_ADD || signalOp == core::atomicType::AMO_SIGNAL_ADD) {
          warp_total_rsn = 1;
        }
      }
      if (isLastChunk && !onlyOneSignal &&
          (signalOp == core::atomicType::AMO_ADD || signalOp == core::atomicType::AMO_SIGNAL_ADD)) {
        warp_total_rsn = num_active_lanes;
        my_rsn_excl = my_logical_lane_id;
      }
    }
    if (is_leader) {
      if constexpr (PrvdType == core::ProviderType::MLX5) {
        warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_wqes, __ATOMIC_RELAXED,
                                                 __HIP_MEMORY_SCOPE_AGENT);
      } else if constexpr (PrvdType == core::ProviderType::JMRD) {
        warp_sq_counter = core::atomic_add_packed_rsn_and_psn(
            *wq, num_wqes, warp_total_psn, warp_total_rsn, &warp_psn_counter, &warp_rsn_counter);
      } else if constexpr (PrvdType == core::ProviderType::BNXT) {
        core::atomic_add_packed_msn_and_psn(&wq->msnPack, num_wqes, warp_total_psn,
                                            &warp_msntbl_counter, &warp_psn_counter);
        warp_sq_counter = warp_msntbl_counter;
        core::AtomicMaxSerial(&wq->postIdx, warp_sq_counter + num_wqes);
      } else if constexpr (PrvdType == core::ProviderType::PSD) {
        warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_wqes, __ATOMIC_RELAXED,
                                                 __HIP_MEMORY_SCOPE_AGENT);
      } else {
        static_assert(false);
      }
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    if constexpr (PrvdType == core::ProviderType::MLX5) {
      my_sq_counter = warp_sq_counter +
                      (isLastChunk && !onlyOneSignal ? my_logical_lane_id * 2 : my_logical_lane_id);
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
      warp_rsn_counter = __shfl(warp_rsn_counter, leader_phys_lane_id);
      my_sq_counter = warp_sq_counter +
                      (isLastChunk && !onlyOneSignal ? my_logical_lane_id * 2 : my_logical_lane_id);
      my_psn_counter = warp_psn_counter + my_psn_excl;
      my_rsn_counter = warp_rsn_counter + my_rsn_excl;
    } else if constexpr (PrvdType == core::ProviderType::BNXT) {
      warp_msntbl_counter = __shfl(warp_msntbl_counter, leader_phys_lane_id);
      warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
      my_sq_counter = warp_sq_counter +
                      (isLastChunk && !onlyOneSignal ? my_logical_lane_id * 2 : my_logical_lane_id);
      my_msntbl_counter =
          warp_msntbl_counter +
          (isLastChunk && !onlyOneSignal ? my_logical_lane_id * 2 : my_logical_lane_id);
      my_psn_counter = warp_psn_counter + my_psn_excl;
    } else if constexpr (PrvdType == core::ProviderType::PSD) {
      my_sq_counter = warp_sq_counter +
                      (isLastChunk && !onlyOneSignal ? my_logical_lane_id * 2 : my_logical_lane_id);
    } else {
      static_assert(false);
    }

    while (true) {
      uint32_t db_touched =
          __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t db_done =
          __hip_atomic_load(&wq->doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t num_active_sq_entries = db_touched - db_done;
      uint32_t num_free_entries = wq->sqWqeNum - num_active_sq_entries;
      uint32_t num_entries_until_warp_last_entry = warp_sq_counter + num_wqes - db_touched;
      if (num_free_entries > num_entries_until_warp_last_entry) {
        break;
      }
      ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
    }

    // Post RDMA write for this chunk
    // Note: PostWrite always returns a valid doorbell value, regardless of cqeSignal
    uint64_t dbr_val;
    if constexpr (PrvdType == core::ProviderType::MLX5) {
      dbr_val = core::PostWrite<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter,
                                          is_leader, qpn, laddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      dbr_val = core::PostWrite<PrvdType>(*wq, my_sq_counter, 0, my_psn_counter, is_leader, qpn,
                                          laddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::BNXT) {
      dbr_val = core::PostWrite<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter,
                                          is_leader, qpn, laddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::PSD) {
      dbr_val = core::PostWrite<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter,
                                          is_leader, qpn, laddr, lkey, raddr, rkey, transfer_size);
    } else {
      static_assert(false);
    }

    // Post signal only for the last chunk (and update dbr_val)
    if (isLastChunk) {
      // assert(signalDestOffset + sizeof(signalValue) <= signalDest->size);
      uintptr_t signalRaddr;
      uint32_t signalRkey;
      if (!needsChunking) {
        signalRaddr = signalDest->peerPtrs[pe] + signalDestOffset;
        signalRkey = signalDest->peerRkeys[pe];
      } else {
        uintptr_t signalAddr = reinterpret_cast<uintptr_t>(signalDest->localPtr) + signalDestOffset;
        VmmLookupRemote(signalAddr, pe, signalRaddr, signalRkey);
      }
      if (signalOp == core::atomicType::AMO_SET || signalOp == core::atomicType::AMO_SIGNAL_SET) {
        // TODO: not support masked atomic yet, use write inline for now
        bool should_signal = onlyOneSignal ? is_leader : true;
        if (should_signal) {
          if constexpr (PrvdType == core::ProviderType::MLX5) {
            dbr_val = core::PostWriteInline<PrvdType>(
                *wq, my_sq_counter + 1, my_sq_counter + 1, my_sq_counter + 1, is_leader, qpn,
                &signalValue, signalRaddr, signalRkey, sizeof(signalValue));
          } else if constexpr (PrvdType == core::ProviderType::JMRD) {
            dbr_val = core::PostWriteInline<PrvdType>(
                *wq, my_sq_counter + 1, 0, my_psn_counter + psnCnt, is_leader, qpn, &signalValue,
                signalRaddr, signalRkey, sizeof(signalValue));
          } else if constexpr (PrvdType == core::ProviderType::BNXT) {
            dbr_val = core::PostWriteInline<PrvdType>(
                *wq, my_sq_counter + 1, my_msntbl_counter + 1, my_psn_counter + psnCnt, is_leader,
                qpn, &signalValue, signalRaddr, signalRkey, sizeof(signalValue));
          } else if constexpr (PrvdType == core::ProviderType::PSD) {
            dbr_val = core::PostWriteInline<PrvdType>(
                *wq, my_sq_counter + 1, my_sq_counter + 1, my_sq_counter + 1, is_leader, qpn,
                &signalValue, signalRaddr, signalRkey, sizeof(signalValue));
          }
        }
      } else if (signalOp == core::atomicType::AMO_ADD ||
                 signalOp == core::atomicType::AMO_SIGNAL_ADD) {
        core::IbufHandle* ibuf = &ep[epIndex].atomicIbuf;
        bool should_signal = onlyOneSignal ? is_leader : true;
        if (should_signal) {
          if constexpr (PrvdType == core::ProviderType::MLX5) {
            dbr_val = core::PostAtomic<PrvdType>(
                *wq, my_sq_counter + 1, my_sq_counter + 1, my_sq_counter + 1, is_leader, qpn,
                ibuf->addr, ibuf->lkey, signalRaddr, signalRkey, &signalValue, &signalValue,
                sizeof(signalValue), core::atomicType::AMO_ADD);
          } else if constexpr (PrvdType == core::ProviderType::JMRD) {
            dbr_val = core::PostAtomic<PrvdType>(
                *wq, my_sq_counter + 1, my_rsn_counter, my_psn_counter + psnCnt, is_leader, qpn,
                ibuf->addr, ibuf->lkey, signalRaddr, signalRkey, &signalValue, &signalValue,
                sizeof(signalValue), core::atomicType::AMO_ADD);
          } else if constexpr (PrvdType == core::ProviderType::BNXT) {
            dbr_val = core::PostAtomic<PrvdType>(
                *wq, my_sq_counter + 1, my_msntbl_counter + 1, my_psn_counter + psnCnt, is_leader,
                qpn, ibuf->addr, ibuf->lkey, signalRaddr, signalRkey, &signalValue, &signalValue,
                sizeof(signalValue), core::atomicType::AMO_ADD);
          } else if constexpr (PrvdType == core::ProviderType::PSD) {
            dbr_val = core::PostAtomic<PrvdType>(
                *wq, my_sq_counter + 1, my_sq_counter + 1, my_sq_counter + 1, is_leader, qpn,
                ibuf->addr, ibuf->lkey, signalRaddr, signalRkey, &signalValue, &signalValue,
                sizeof(signalValue), core::atomicType::AMO_ADD);
          }
        }
      } else {
        assert(false);
      }
    }

    __threadfence_system();
    if (is_leader) {
      uint64_t db_touched{0};
      do {
        db_touched = __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      } while (db_touched != warp_sq_counter);

      core::UpdateSendDbrRecord<PrvdType>(wq->dbrRecAddr, warp_sq_counter + num_wqes);
      __threadfence_system();
      // Ring doorbell every iteration (dbr_val comes from PostWrite or signal WQE)
      core::RingDoorbell<PrvdType>(wq->dbrAddr, dbr_val);
      __threadfence_system();

      if (isLastChunk) {
        __hip_atomic_fetch_add(&cq->needConsIdx, 2, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      } else {
        __hip_atomic_fetch_add(&cq->needConsIdx, 1, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      }
      __hip_atomic_store(&wq->dbTouchIdx, warp_sq_counter + num_wqes, __ATOMIC_RELAXED,
                         __HIP_MEMORY_SCOPE_AGENT);
    }
    __threadfence_system();

    // Move to next chunk
    currentOffset += transfer_size;
    remaining -= transfer_size;
  }
}

template <>
inline __device__ void ShmemPutMemNbiSignalThreadKernel<application::TransportType::RDMA, true>(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes,
    const application::SymmMemObjPtr signalDest, size_t signalDestOffset, uint64_t signalValue,
    core::atomicType signalOp, int pe, int qpId) {
  assert(ShmemPeerHasQp(pe) && "RDMA put+signal to a peer with no QP (rail-only stub?)");
  bool need_turn{true};
  uint64_t turns = __ballot(need_turn);
  while (turns) {
    uint8_t lane = __ffsll((unsigned long long)turns) - 1;
    int pe_turn = __shfl(pe, lane);
    if (pe_turn == pe) {
      DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(
          ShmemPutMemNbiSignalThreadKernelImpl, true, dest, destOffset, source, sourceOffset, bytes,
          signalDest, signalDestOffset, signalValue, signalOp, pe, qpId);
      need_turn = false;
    }
    turns = __ballot(need_turn);
  }
}

template <>
inline __device__ void ShmemPutMemNbiSignalThreadKernel<application::TransportType::RDMA, false>(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes,
    const application::SymmMemObjPtr signalDest, size_t signalDestOffset, uint64_t signalValue,
    core::atomicType signalOp, int pe, int qpId) {
  bool need_turn{true};
  uint64_t turns = __ballot(need_turn);
  while (turns) {
    uint8_t lane = __ffsll((unsigned long long)turns) - 1;
    int pe_turn = __shfl(pe, lane);
    if (pe_turn == pe) {
      DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(
          ShmemPutMemNbiSignalThreadKernelImpl, false, dest, destOffset, source, sourceOffset,
          bytes, signalDest, signalDestOffset, signalValue, signalOp, pe, qpId);
      need_turn = false;
    }
    turns = __ballot(need_turn);
  }
}

template <core::ProviderType PrvdType, bool onlyOneSignal = true>
inline __device__ void ShmemPutMemNbiSignalWarpKernelImpl(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes,
    const application::SymmMemObjPtr signalDest, size_t signalDestOffset, uint64_t signalValue,
    core::atomicType signalOp, int pe, int qpId) {
  int laneId = threadIdx.x & (warpSize - 1);
  if (laneId == 0) {
    ShmemPutMemNbiSignalThreadKernelImpl<PrvdType, onlyOneSignal>(
        dest, destOffset, source, sourceOffset, bytes, signalDest, signalDestOffset, signalValue,
        signalOp, pe, qpId);
  }
}

template <core::ProviderType PrvdType, bool onlyOneSignal = true>
inline __device__ void ShmemPutMemNbiSignalBlockKernelImpl(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes,
    const application::SymmMemObjPtr signalDest, size_t signalDestOffset, uint64_t signalValue,
    core::atomicType signalOp, int pe, int qpId) {
  if (core::FlatBlockThreadId() == 0) {
    ShmemPutMemNbiSignalThreadKernelImpl<PrvdType, onlyOneSignal>(
        dest, destOffset, source, sourceOffset, bytes, signalDest, signalDestOffset, signalValue,
        signalOp, pe, qpId);
  }
}

template <>
inline __device__ void ShmemPutMemNbiSignalWarpKernel<application::TransportType::RDMA, true>(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes,
    const application::SymmMemObjPtr signalDest, size_t signalDestOffset, uint64_t signalValue,
    core::atomicType signalOp, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(ShmemPutMemNbiSignalWarpKernelImpl, true, dest,
                                                destOffset, source, sourceOffset, bytes, signalDest,
                                                signalDestOffset, signalValue, signalOp, pe, qpId);
}

template <>
inline __device__ void ShmemPutMemNbiSignalWarpKernel<application::TransportType::RDMA, false>(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes,
    const application::SymmMemObjPtr signalDest, size_t signalDestOffset, uint64_t signalValue,
    core::atomicType signalOp, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(ShmemPutMemNbiSignalWarpKernelImpl, false, dest,
                                                destOffset, source, sourceOffset, bytes, signalDest,
                                                signalDestOffset, signalValue, signalOp, pe, qpId);
}

template <>
inline __device__ void ShmemPutMemNbiSignalBlockKernel<application::TransportType::RDMA, true>(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes,
    const application::SymmMemObjPtr signalDest, size_t signalDestOffset, uint64_t signalValue,
    core::atomicType signalOp, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(ShmemPutMemNbiSignalBlockKernelImpl, true, dest,
                                                destOffset, source, sourceOffset, bytes, signalDest,
                                                signalDestOffset, signalValue, signalOp, pe, qpId);
}

template <>
inline __device__ void ShmemPutMemNbiSignalBlockKernel<application::TransportType::RDMA, false>(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes,
    const application::SymmMemObjPtr signalDest, size_t signalDestOffset, uint64_t signalValue,
    core::atomicType signalOp, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(ShmemPutMemNbiSignalBlockKernelImpl, false, dest,
                                                destOffset, source, sourceOffset, bytes, signalDest,
                                                signalDestOffset, signalValue, signalOp, pe, qpId);
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemAtomicSizeNonFetchThreadKernelImpl(
    const application::SymmMemObjPtr dest, size_t destOffset, void* val, size_t bytes,
    core::atomicType amoType, int pe, int qpId) {
  if (bytes == 0) return;
  // assert(destOffset + bytes <= dest->size);

  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle* wq = &ep[epIndex].wqHandle;
  core::CompletionQueueHandle* cq = &ep[epIndex].cqHandle;
  uint32_t qpn = ep[epIndex].qpn;
  core::IbufHandle* ibuf = &ep[epIndex].atomicIbuf;

  // Get correct rkey for VMM heap or use direct rkey for Isolation/Static Heap
  uintptr_t raddr;
  uint32_t rkey;
  if (globalGpuStates->useVMMHeap) {
    // VMM Heap: atomic data is small (≤8 bytes), won't cross chunk boundary
    uintptr_t dstAddr = reinterpret_cast<uintptr_t>(dest->localPtr) + destOffset;
    VmmLookupRemote(dstAddr, pe, raddr, rkey);
  } else {
    // Isolation or Static Heap: direct access
    raddr = dest->peerPtrs[pe] + destOffset;
    rkey = dest->peerRkeys[pe];
  }

  uintptr_t laddr = ibuf->addr;
  uintptr_t lkey = ibuf->lkey;

  uint64_t activemask = core::GetActiveLaneMask();
  uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
  uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
  bool is_leader{my_logical_lane_id == num_active_lanes - 1};
  const uint64_t leader_phys_lane_id = core::GetLastActiveLaneID(activemask);

  uint32_t warp_sq_counter = 0;
  uint32_t warp_msntbl_counter = 0, warp_psn_counter = 0, warp_rsn_counter = 0;
  uint32_t my_sq_counter = 0, my_msntbl_counter = 0, my_psn_counter = 0, my_rsn_counter = 0;

  if constexpr (PrvdType == core::ProviderType::MLX5) {
    if (is_leader) {
      warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                               __HIP_MEMORY_SCOPE_AGENT);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    if (is_leader) {
      warp_sq_counter = core::atomic_add_packed_rsn_and_psn(*wq, num_active_lanes, num_active_lanes,
                                                            num_active_lanes, &warp_psn_counter,
                                                            &warp_rsn_counter);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
    warp_rsn_counter = __shfl(warp_rsn_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
    my_psn_counter = warp_psn_counter + my_logical_lane_id;
    my_rsn_counter = warp_rsn_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::BNXT) {
    if (is_leader) {
      core::atomic_add_packed_msn_and_psn(&wq->msnPack, num_active_lanes, num_active_lanes,
                                          &warp_msntbl_counter, &warp_psn_counter);
      warp_sq_counter = warp_msntbl_counter;
      core::AtomicMaxSerial(&wq->postIdx, warp_sq_counter + num_active_lanes);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    warp_msntbl_counter = __shfl(warp_msntbl_counter, leader_phys_lane_id);
    warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
    my_msntbl_counter = warp_msntbl_counter + my_logical_lane_id;
    my_psn_counter = warp_psn_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    if (is_leader) {
      warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                               __HIP_MEMORY_SCOPE_AGENT);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
  } else {
    static_assert(false);
  }

  while (true) {
    uint32_t db_touched =
        __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t db_done = __hip_atomic_load(&wq->doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t num_active_sq_entries = db_touched - db_done;
    uint32_t num_free_entries = wq->sqWqeNum - num_active_sq_entries;
    uint32_t num_entries_until_warp_last_entry = warp_sq_counter + num_active_lanes - db_touched;
    if (num_free_entries > num_entries_until_warp_last_entry) break;
    ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
  }

  uint64_t dbr_val;
  if constexpr (PrvdType == core::ProviderType::MLX5) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader, qpn,
                                   laddr, lkey, raddr, rkey, val, val, bytes, amoType);
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_rsn_counter, my_psn_counter, is_leader,
                                   qpn, laddr, lkey, raddr, rkey, val, val, bytes, amoType);
  } else if constexpr (PrvdType == core::ProviderType::BNXT) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter, is_leader,
                                   qpn, laddr, lkey, raddr, rkey, val, val, bytes, amoType);
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader, qpn,
                                   laddr, lkey, raddr, rkey, val, val, bytes, amoType);
  }

  __threadfence_system();
  if (is_leader) {
    uint64_t db_touched = 0;
    do {
      db_touched = __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    } while (db_touched != warp_sq_counter);

    core::UpdateSendDbrRecord<PrvdType>(wq->dbrRecAddr, warp_sq_counter + num_active_lanes);
    __threadfence_system();
    core::RingDoorbell<PrvdType>(wq->dbrAddr, dbr_val);

    __hip_atomic_fetch_add(&cq->needConsIdx, 1, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    __hip_atomic_store(&wq->dbTouchIdx, warp_sq_counter + num_active_lanes, __ATOMIC_RELAXED,
                       __HIP_MEMORY_SCOPE_AGENT);
  }

  __threadfence_system();
}

template <>
inline __device__ void ShmemAtomicSizeNonFetchThreadKernel<application::TransportType::RDMA>(
    const application::SymmMemObjPtr dest, size_t destOffset, void* val, size_t bytes,
    core::atomicType amoType, int pe, int qpId) {
  assert(ShmemPeerHasQp(pe) && "RDMA atomic to a peer with no QP (rail-only stub?)");
  bool need_turn{true};
  uint64_t turns = __ballot(need_turn);
  while (turns) {
    uint8_t lane = __ffsll((unsigned long long)turns) - 1;
    int pe_turn = __shfl(pe, lane);
    if (pe_turn == pe) {
      DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemAtomicSizeNonFetchThreadKernelImpl, dest, destOffset,
                                          val, bytes, amoType, pe, qpId);
      need_turn = false;
    }
    turns = __ballot(need_turn);
  }
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemAtomicSizeNonFetchWarpKernelImpl(const application::SymmMemObjPtr dest,
                                                             size_t destOffset, void* val,
                                                             size_t bytes, core::atomicType amoType,
                                                             int pe, int qpId) {
  int laneId = threadIdx.x & (warpSize - 1);
  if (laneId == 0) {
    ShmemAtomicSizeNonFetchThreadKernelImpl<PrvdType>(dest, destOffset, val, bytes, amoType, pe,
                                                      qpId);
  }
}

template <>
inline __device__ void ShmemAtomicSizeNonFetchWarpKernel<application::TransportType::RDMA>(
    const application::SymmMemObjPtr dest, size_t destOffset, void* val, size_t bytes,
    core::atomicType amoType, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemAtomicSizeNonFetchWarpKernelImpl, dest, destOffset, val,
                                      bytes, amoType, pe, qpId);
}

inline __device__ uint32_t ShmemGetAtomicIbufSlot(core::IbufHandle* ibuf, uint32_t num_slots = 1) {
  uint32_t base_slot = atomicAdd(&ibuf->head, num_slots);
  uint32_t nslots = ibuf->nslots;
  uint32_t last_slot = base_slot + num_slots;
  while (last_slot - __hip_atomic_load(&ibuf->tail, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT) >
         nslots) {
    ;
  }
  __threadfence_block();
  return base_slot;
}

inline __device__ void ShmemReleaseAtomicIbufSlot(core::IbufHandle* ibuf, uint32_t base_slots,
                                                  uint32_t num_slots) {
  uint32_t last_slot = base_slots + num_slots;
  while (atomicCAS(&ibuf->tail, base_slots, last_slot) != base_slots) {
    ;
  }
  __threadfence_block();
}

template <core::ProviderType PrvdType, typename T>
inline __device__ T ShmemAtomicTypeFetchThreadKernelImpl(const application::SymmMemObjPtr dest,
                                                         size_t destOffset, void* val,
                                                         void* compare, size_t bytes,
                                                         core::atomicType amoType, int pe,
                                                         int qpId) {
  // assert(destOffset + bytes <= dest->size);
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle* wq = &ep[epIndex].wqHandle;
  core::CompletionQueueHandle* cq = &ep[epIndex].cqHandle;
  uint32_t qpn = ep[epIndex].qpn;
  core::IbufHandle* ibuf = &ep[epIndex].atomicIbuf;

  uint64_t activemask = core::GetActiveLaneMask();
  uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
  uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
  bool is_leader = (my_logical_lane_id == num_active_lanes - 1);
  uint64_t leader_phys_lane_id = core::GetLastActiveLaneID(activemask);

  uint32_t base_slot = 0;
  if (is_leader) {
    base_slot = ShmemGetAtomicIbufSlot(ibuf, num_active_lanes);
  }
  uint32_t my_slot = __shfl(base_slot, leader_phys_lane_id) + my_logical_lane_id;
  uint32_t my_slot_index = my_slot & (ibuf->nslots - 1);
  uintptr_t laddr = ibuf->addr + (my_slot_index + 1) * application::ATOMIC_IBUF_SLOT_SIZE;
  uintptr_t lkey = ibuf->lkey;

  // Get correct rkey for VMM heap or use direct rkey for Isolation/Static Heap
  uintptr_t raddr;
  uint32_t rkey;
  if (globalGpuStates->useVMMHeap) {
    // VMM Heap: atomic data is small (≤8 bytes), won't cross chunk boundary
    uintptr_t dstAddr = reinterpret_cast<uintptr_t>(dest->localPtr) + destOffset;
    VmmLookupRemote(dstAddr, pe, raddr, rkey);
  } else {
    // Isolation or Static Heap: direct access
    raddr = dest->peerPtrs[pe] + destOffset;
    rkey = dest->peerRkeys[pe];
  }

  uint32_t warp_sq_counter = 0;
  uint32_t warp_msntbl_counter = 0, warp_psn_counter = 0, warp_rsn_counter = 0;
  uint32_t my_sq_counter = 0, my_msntbl_counter = 0, my_psn_counter = 0, my_rsn_counter = 0;

  if constexpr (PrvdType == core::ProviderType::MLX5) {
    if (is_leader) {
      warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                               __HIP_MEMORY_SCOPE_AGENT);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    if (is_leader) {
      warp_sq_counter = core::atomic_add_packed_rsn_and_psn(*wq, num_active_lanes, num_active_lanes,
                                                            num_active_lanes, &warp_psn_counter,
                                                            &warp_rsn_counter);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
    warp_rsn_counter = __shfl(warp_rsn_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
    my_psn_counter = warp_psn_counter + my_logical_lane_id;
    my_rsn_counter = warp_rsn_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::BNXT) {
    if (is_leader) {
      core::atomic_add_packed_msn_and_psn(&wq->msnPack, num_active_lanes, num_active_lanes,
                                          &warp_msntbl_counter, &warp_psn_counter);
      warp_sq_counter = warp_msntbl_counter;
      core::AtomicMaxSerial(&wq->postIdx, warp_sq_counter + num_active_lanes);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    warp_msntbl_counter = __shfl(warp_msntbl_counter, leader_phys_lane_id);
    warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
    my_msntbl_counter = warp_msntbl_counter + my_logical_lane_id;
    my_psn_counter = warp_psn_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    if (is_leader) {
      warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                               __HIP_MEMORY_SCOPE_AGENT);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
  } else {
    static_assert(false);
  }

  while (true) {
    uint32_t db_touched =
        __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t db_done = __hip_atomic_load(&wq->doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t num_active_sq_entries = db_touched - db_done;
    uint32_t num_free_entries = wq->sqWqeNum - num_active_sq_entries;
    uint32_t num_entries_until_warp_last_entry = warp_sq_counter + num_active_lanes - db_touched;
    if (num_free_entries > num_entries_until_warp_last_entry) break;
    ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
  }

  uint64_t dbr_val;
  if constexpr (PrvdType == core::ProviderType::MLX5) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader, qpn,
                                   laddr, lkey, raddr, rkey, val, compare, bytes, amoType);
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_rsn_counter, my_psn_counter, is_leader,
                                   qpn, laddr, lkey, raddr, rkey, val, compare, bytes, amoType);
  } else if constexpr (PrvdType == core::ProviderType::BNXT) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter, is_leader,
                                   qpn, laddr, lkey, raddr, rkey, val, compare, bytes, amoType);
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader, qpn,
                                   laddr, lkey, raddr, rkey, val, compare, bytes, amoType);
  }

  __threadfence_system();
  if (is_leader) {
    uint64_t db_touched = 0;
    do {
      db_touched = __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    } while (db_touched != warp_sq_counter);

    core::UpdateSendDbrRecord<PrvdType>(wq->dbrRecAddr, warp_sq_counter + num_active_lanes);
    __threadfence_system();
    core::RingDoorbell<PrvdType>(wq->dbrAddr, dbr_val);

    __hip_atomic_fetch_add(&cq->needConsIdx, 1, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    __hip_atomic_store(&wq->dbTouchIdx, warp_sq_counter + num_active_lanes, __ATOMIC_RELAXED,
                       __HIP_MEMORY_SCOPE_AGENT);
  }

  ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
  T ret = *reinterpret_cast<volatile T*>(laddr);
  if (sizeof(T) == 4) ret = BSWAP32((uint32_t)ret);

  if (is_leader) {
    ShmemReleaseAtomicIbufSlot(ibuf, base_slot, num_active_lanes);
  }

  return ret;
}

#define DEFINE_SHMEM_ATOMIC_TYPE_FETCH_THREAD_KERNEL(TypeName, T)                            \
  template <>                                                                                \
  inline __device__ T ShmemAtomicTypeFetchThreadKernel<application::TransportType::RDMA, T>( \
      const application::SymmMemObjPtr dest, size_t destOffset, void* val, void* compare,    \
      size_t bytes, core::atomicType amoType, int pe, int qpId) {                            \
    bool need_turn{true};                                                                    \
    uint64_t turns = __ballot(need_turn);                                                    \
    T result{};                                                                              \
    while (turns) {                                                                          \
      uint8_t lane = __ffsll((unsigned long long)turns) - 1;                                 \
      int pe_turn = __shfl(pe, lane);                                                        \
      if (pe_turn == pe) {                                                                   \
        result = DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_RETURN(                            \
            ShmemAtomicTypeFetchThreadKernelImpl, T, dest, destOffset, val, compare, bytes,  \
            amoType, pe, qpId);                                                              \
        need_turn = false;                                                                   \
      }                                                                                      \
      turns = __ballot(need_turn);                                                           \
    }                                                                                        \
    return result;                                                                           \
  }

DEFINE_SHMEM_ATOMIC_TYPE_FETCH_THREAD_KERNEL(Uint32, uint32_t)
DEFINE_SHMEM_ATOMIC_TYPE_FETCH_THREAD_KERNEL(Uint64, uint64_t)
DEFINE_SHMEM_ATOMIC_TYPE_FETCH_THREAD_KERNEL(Int32, int32_t)
DEFINE_SHMEM_ATOMIC_TYPE_FETCH_THREAD_KERNEL(Int64, int64_t)

template <core::ProviderType PrvdType, typename T>
inline __device__ T ShmemAtomicTypeFetchWarpKernelImpl(const application::SymmMemObjPtr dest,
                                                       size_t destOffset, void* val, void* compare,
                                                       size_t bytes, core::atomicType amoType,
                                                       int pe, int qpId) {
  int laneId = threadIdx.x & (warpSize - 1);
  if (laneId == 0) {
    return ShmemAtomicTypeFetchThreadKernelImpl<PrvdType, T>(dest, destOffset, val, compare, bytes,
                                                             amoType, pe, qpId);
  }
  return T{};
}

#define DEFINE_SHMEM_ATOMIC_TYPE_FETCH_WARP_KERNEL(TypeName, T)                                   \
  template <>                                                                                     \
  inline __device__ T ShmemAtomicTypeFetchWarpKernel<application::TransportType::RDMA, T>(        \
      const application::SymmMemObjPtr dest, size_t destOffset, void* val, void* compare,         \
      size_t bytes, core::atomicType amoType, int pe, int qpId) {                                 \
    return DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_RETURN(ShmemAtomicTypeFetchWarpKernelImpl, T, \
                                                           dest, destOffset, val, compare, bytes, \
                                                           amoType, pe, qpId);                    \
  }

DEFINE_SHMEM_ATOMIC_TYPE_FETCH_WARP_KERNEL(Uint32, uint32_t)
DEFINE_SHMEM_ATOMIC_TYPE_FETCH_WARP_KERNEL(Uint64, uint64_t)
DEFINE_SHMEM_ATOMIC_TYPE_FETCH_WARP_KERNEL(Int32, int32_t)
DEFINE_SHMEM_ATOMIC_TYPE_FETCH_WARP_KERNEL(Int64, int64_t)

/* ---------------------------------------------------------------------------------------------- */
/*                      Pure Address-Based RDMA Kernels (New API)                                 */
/* ---------------------------------------------------------------------------------------------- */

// Convert SHMEM heap address to remote address and key
// Supports both Static Heap and VMM Heap modes
inline __device__ void QueryRemoteAddr(const void* localAddr, int pe, uintptr_t& out_raddr,
                                       uint32_t& out_rkey) {
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  uintptr_t localAddrInt = reinterpret_cast<uintptr_t>(localAddr);

  if (globalGpuStates->heapObj == nullptr) {
    assert(false);
    out_raddr = 0;
    out_rkey = 0;
    return;
  }

  // Check if address is in symmetric heap range
  if (localAddrInt < globalGpuStates->heapBaseAddr ||
      localAddrInt >= globalGpuStates->heapEndAddr) {
    assert(false);
    out_raddr = 0;
    out_rkey = 0;
    return;
  }

  // Calculate offset within the symmetric heap
  size_t offset = localAddrInt - globalGpuStates->heapBaseAddr;
  application::SymmMemObj* heapObj = globalGpuStates->heapObj;

  if (globalGpuStates->useVMMHeap) {
    // VMM Heap: need to get chunk-specific rkey
    VmmLookupRemote(localAddrInt, pe, out_raddr, out_rkey);
  } else {
    // Static Heap: direct rkey access
    out_raddr = heapObj->peerPtrs[pe] + offset;
    out_rkey = heapObj->peerRkeys[pe];
  }
}

// New pure address-based PutMemNbi kernel for RDMA
template <core::ProviderType PrvdType>
inline __device__ void ShmemPutMemNbiThreadKernelAddrImpl(const void* dest, const void* source,
                                                          size_t bytes, int pe, int qpId) {
  if (bytes == 0) return;

  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle* wq = &ep[epIndex].wqHandle;
  core::CompletionQueueHandle* cq = &ep[epIndex].cqHandle;
  uint32_t qpn = ep[epIndex].qpn;

  // Determine if chunking is needed (VMM heap mode)
  bool needsChunking = globalGpuStates->useVMMHeap;

  uintptr_t srcStartAddr = reinterpret_cast<uintptr_t>(source);
  uintptr_t dstStartAddr = reinterpret_cast<uintptr_t>(dest);
  size_t remaining = bytes;
  size_t currentOffset = 0;

  // Main loop: process one chunk per iteration
  while (true) {
    // Check if current thread still has data to transfer
    bool has_remaining = (remaining > 0);

    // Synchronize within warp: get mask of threads that still have work
    uint64_t activemask = __ballot(has_remaining);
    if (activemask == 0) {
      break;  // All threads in warp are done
    }

    // Recalculate active lane info for threads with remaining data
    uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
    uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
    bool is_leader{my_logical_lane_id == num_active_lanes - 1};
    const uint64_t leader_phys_lane_id = core::GetLastActiveLaneID(activemask);

    // Inactive threads skip actual work but stay synchronized
    if (!has_remaining) {
      continue;
    }

    // Determine transfer size for this chunk
    uintptr_t srcAddr = srcStartAddr + currentOffset;
    uintptr_t dstAddr = dstStartAddr + currentOffset;
    uint32_t lkey = globalGpuStates->heapObj->lkey;
    uintptr_t raddr;
    uint32_t rkey;
    size_t transfer_size;

    if (!needsChunking) {
      // Static Heap: single transfer, direct rkey access
      transfer_size = remaining;
      size_t offset = dstAddr - globalGpuStates->heapBaseAddr;
      raddr = globalGpuStates->heapObj->peerPtrs[pe] + offset;
      rkey = globalGpuStates->heapObj->peerRkeys[pe];
    } else {
      // VMM Heap: get chunk-specific rkey and size
      size_t src_chunk_size, dst_chunk_size;
      VmmQueryLocalKey(srcAddr, remaining, lkey, src_chunk_size);
      VmmQueryRemoteAddr(dstAddr, pe, remaining, raddr, rkey, dst_chunk_size);
      transfer_size = src_chunk_size < dst_chunk_size ? src_chunk_size : dst_chunk_size;
    }
    MORI_PRINTF("blockIdx.x=%d, threadIdx.x=%d, remaining=%zu, transfer_size=%zu\n", blockIdx.x,
                threadIdx.x, remaining, transfer_size);
    uint32_t warp_sq_counter{0};
    uint32_t warp_msntbl_counter{0}, warp_psn_counter{0};
    uint32_t my_sq_counter{0}, my_msntbl_counter{0}, my_psn_counter{0};
    uint32_t psnCnt = 0;
    uint32_t warp_total_psn = 0, my_psn_excl = 0;

    if constexpr (PrvdType == core::ProviderType::BNXT) {
      psnCnt = (transfer_size + wq->mtuSize - 1) / wq->mtuSize;
      my_psn_excl = WarpActivePsnPrefix(psnCnt, activemask, &warp_total_psn);
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      psnCnt = (transfer_size + wq->mtuSize - 1) / wq->mtuSize;
      my_psn_excl = WarpActivePsnPrefix(psnCnt, activemask, &warp_total_psn);
    }
    if (is_leader) {
      if constexpr (PrvdType == core::ProviderType::MLX5) {
        warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                                 __HIP_MEMORY_SCOPE_AGENT);
      } else if constexpr (PrvdType == core::ProviderType::JMRD) {
        warp_sq_counter = core::atomic_add_packed_rsn_and_psn(*wq, num_active_lanes, warp_total_psn,
                                                              &warp_psn_counter);
      } else if constexpr (PrvdType == core::ProviderType::BNXT) {
        core::atomic_add_packed_msn_and_psn(&wq->msnPack, num_active_lanes, warp_total_psn,
                                            &warp_msntbl_counter, &warp_psn_counter);
        warp_sq_counter = warp_msntbl_counter;
        core::AtomicMaxSerial(&wq->postIdx, warp_sq_counter + num_active_lanes);
      } else if constexpr (PrvdType == core::ProviderType::PSD) {
        warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                                 __HIP_MEMORY_SCOPE_AGENT);
      } else {
        static_assert(false);
      }
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    if constexpr (PrvdType == core::ProviderType::MLX5) {
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
      warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
      my_psn_counter = warp_psn_counter + my_psn_excl;
    } else if constexpr (PrvdType == core::ProviderType::BNXT) {
      warp_msntbl_counter = __shfl(warp_msntbl_counter, leader_phys_lane_id);
      warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
      my_msntbl_counter = warp_msntbl_counter + my_logical_lane_id;
      my_psn_counter = warp_psn_counter + my_psn_excl;
    } else if constexpr (PrvdType == core::ProviderType::PSD) {
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
    } else {
      static_assert(false);
    }

    while (true) {
      uint32_t db_touched =
          __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t db_done =
          __hip_atomic_load(&wq->doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t num_active_sq_entries = db_touched - db_done;
      uint32_t num_free_entries = wq->sqWqeNum - num_active_sq_entries;
      uint32_t num_entries_until_warp_last_entry = warp_sq_counter + num_active_lanes - db_touched;
      if (num_free_entries > num_entries_until_warp_last_entry) {
        break;
      }
      ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
    }

    // Post RDMA write for this chunk
    uint64_t dbr_val;
    if constexpr (PrvdType == core::ProviderType::MLX5) {
      dbr_val =
          core::PostWrite<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader,
                                    qpn, srcAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      dbr_val =
          core::PostWrite<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter,
                                    is_leader, qpn, srcAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::BNXT) {
      dbr_val =
          core::PostWrite<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter,
                                    is_leader, qpn, srcAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::PSD) {
      dbr_val =
          core::PostWrite<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader,
                                    qpn, srcAddr, lkey, raddr, rkey, transfer_size);
    } else {
      static_assert(false);
    }

    __threadfence_system();
    if (is_leader) {
      uint64_t db_touched{0};
      do {
        db_touched = __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      } while (db_touched != warp_sq_counter);

      core::UpdateSendDbrRecord<PrvdType>(wq->dbrRecAddr, warp_sq_counter + num_active_lanes);
      __threadfence_system();
      core::RingDoorbell<PrvdType>(wq->dbrAddr, dbr_val);
      __threadfence_system();

      __hip_atomic_fetch_add(&cq->needConsIdx, 1, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      __hip_atomic_store(&wq->dbTouchIdx, warp_sq_counter + num_active_lanes, __ATOMIC_RELAXED,
                         __HIP_MEMORY_SCOPE_AGENT);
    }
    __threadfence_system();

    // Move to next chunk
    currentOffset += transfer_size;
    remaining -= transfer_size;
  }
}

template <>
inline __device__ void ShmemPutMemNbiThreadKernel<application::TransportType::RDMA>(
    const void* dest, const void* source, size_t bytes, int pe, int qpId) {
  bool need_turn{true};
  uint64_t turns = __ballot(need_turn);
  while (turns) {
    uint8_t lane = __ffsll((unsigned long long)turns) - 1;
    int pe_turn = __shfl(pe, lane);
    if (pe_turn == pe) {
      DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemPutMemNbiThreadKernelAddrImpl, dest, source, bytes,
                                          pe, qpId);
      need_turn = false;
    }
    turns = __ballot(need_turn);
  }
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemPutMemNbiWarpKernelAddrImpl(const void* dest, const void* source,
                                                        size_t bytes, int pe, int qpId) {
  int laneId = threadIdx.x & (warpSize - 1);
  if (laneId == 0) {
    ShmemPutMemNbiThreadKernelAddrImpl<PrvdType>(dest, source, bytes, pe, qpId);
  }
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemPutMemNbiBlockKernelAddrImpl(const void* dest, const void* source,
                                                         size_t bytes, int pe, int qpId) {
  if (core::FlatBlockThreadId() == 0) {
    ShmemPutMemNbiThreadKernelAddrImpl<PrvdType>(dest, source, bytes, pe, qpId);
  }
}

template <>
inline __device__ void ShmemPutMemNbiWarpKernel<application::TransportType::RDMA>(
    const void* dest, const void* source, size_t bytes, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemPutMemNbiWarpKernelAddrImpl, dest, source, bytes, pe,
                                      qpId);
}

template <>
inline __device__ void ShmemPutMemNbiBlockKernel<application::TransportType::RDMA>(
    const void* dest, const void* source, size_t bytes, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemPutMemNbiBlockKernelAddrImpl, dest, source, bytes, pe,
                                      qpId);
}

// New pure address-based PutSizeImmNbi kernel for RDMA
template <core::ProviderType PrvdType>
inline __device__ void ShmemPutSizeImmNbiThreadKernelAddrImpl(const void* dest, void* val,
                                                              size_t bytes, int pe, int qpId) {
  if (bytes == 0) return;

  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle* wq = &ep[epIndex].wqHandle;
  core::CompletionQueueHandle* cq = &ep[epIndex].cqHandle;
  uint32_t qpn = ep[epIndex].qpn;

  // Convert addresses to remote addresses (supports both Static Heap and VMM Heap)
  uintptr_t raddr;
  uint32_t rkey;
  QueryRemoteAddr(dest, pe, raddr, rkey);

  uint64_t activemask = core::GetActiveLaneMask();
  uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
  uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
  bool is_leader{my_logical_lane_id == num_active_lanes - 1};
  const uint64_t leader_phys_lane_id = core::GetLastActiveLaneID(activemask);
  uint32_t warp_sq_counter{0};
  uint32_t warp_msntbl_counter{0}, warp_psn_counter{0};
  uint32_t my_sq_counter{0}, my_msntbl_counter{0}, my_psn_counter{0};

  if constexpr (PrvdType == core::ProviderType::MLX5) {
    if (is_leader) {
      warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                               __HIP_MEMORY_SCOPE_AGENT);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    if (is_leader) {
      warp_sq_counter = core::atomic_add_packed_rsn_and_psn(*wq, num_active_lanes, num_active_lanes,
                                                            &warp_psn_counter);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
    my_psn_counter = warp_psn_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::BNXT) {
    if (is_leader) {
      core::atomic_add_packed_msn_and_psn(&wq->msnPack, num_active_lanes, num_active_lanes,
                                          &warp_msntbl_counter, &warp_psn_counter);
      warp_sq_counter = warp_msntbl_counter;
      core::AtomicMaxSerial(&wq->postIdx, warp_sq_counter + num_active_lanes);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    warp_msntbl_counter = __shfl(warp_msntbl_counter, leader_phys_lane_id);
    warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
    my_msntbl_counter = warp_msntbl_counter + my_logical_lane_id;
    my_psn_counter = warp_psn_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    if (is_leader) {
      warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                               __HIP_MEMORY_SCOPE_AGENT);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
  } else {
    static_assert(false);
  }

  while (true) {
    uint32_t db_touched =
        __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t db_done = __hip_atomic_load(&wq->doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t num_active_sq_entries = db_touched - db_done;
    uint32_t num_free_entries = wq->sqWqeNum - num_active_sq_entries;
    uint32_t num_entries_until_warp_last_entry = warp_sq_counter + num_active_lanes - db_touched;
    if (num_free_entries > num_entries_until_warp_last_entry) {
      break;
    }
    ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
  }

  uint64_t dbr_val;
  if constexpr (PrvdType == core::ProviderType::MLX5) {
    dbr_val = core::PostWriteInline<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter,
                                              is_leader, qpn, val, raddr, rkey, bytes);
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    dbr_val = core::PostWriteInline<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter,
                                              is_leader, qpn, val, raddr, rkey, bytes);
  } else if constexpr (PrvdType == core::ProviderType::BNXT) {
    dbr_val = core::PostWriteInline<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter,
                                              is_leader, qpn, val, raddr, rkey, bytes);
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    dbr_val = core::PostWriteInline<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter,
                                              is_leader, qpn, val, raddr, rkey, bytes);
  } else {
    static_assert(false);
  }

  if (is_leader) {
    uint64_t db_touched = 0;
    do {
      db_touched = __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    } while (db_touched != warp_sq_counter);

    core::UpdateSendDbrRecord<PrvdType>(wq->dbrRecAddr, warp_sq_counter + num_active_lanes);
    // __threadfence_system();
    core::RingDoorbell<PrvdType>(wq->dbrAddr, dbr_val);
    // __threadfence_system();

    __hip_atomic_fetch_add(&cq->needConsIdx, 1, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    __hip_atomic_store(&wq->dbTouchIdx, warp_sq_counter + num_active_lanes, __ATOMIC_RELAXED,
                       __HIP_MEMORY_SCOPE_AGENT);
  }
}

template <>
inline __device__ void ShmemPutSizeImmNbiThreadKernel<application::TransportType::RDMA>(
    const void* dest, void* val, size_t bytes, int pe, int qpId) {
  bool need_turn{true};
  uint64_t turns = __ballot(need_turn);
  while (turns) {
    uint8_t lane = __ffsll((unsigned long long)turns) - 1;
    int pe_turn = __shfl(pe, lane);
    if (pe_turn == pe) {
      DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemPutSizeImmNbiThreadKernelAddrImpl, dest, val, bytes,
                                          pe, qpId);
      need_turn = false;
    }
    turns = __ballot(need_turn);
  }
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemPutSizeImmNbiWarpKernelAddrImpl(const void* dest, void* val,
                                                            size_t bytes, int pe, int qpId) {
  int laneId = threadIdx.x & (warpSize - 1);
  if (laneId == 0) {
    ShmemPutSizeImmNbiThreadKernelAddrImpl<PrvdType>(dest, val, bytes, pe, qpId);
  }
}

template <>
inline __device__ void ShmemPutSizeImmNbiWarpKernel<application::TransportType::RDMA>(
    const void* dest, void* val, size_t bytes, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemPutSizeImmNbiWarpKernelAddrImpl, dest, val, bytes, pe,
                                      qpId);
}

template <core::ProviderType PrvdType, bool onlyOneSignal = true>
inline __device__ void ShmemPutMemNbiSignalThreadKernelAddrImpl(
    const void* dest, const void* source, size_t bytes, const void* signalDest,
    uint64_t signalValue, core::atomicType signalOp, int pe, int qpId) {
  if (bytes == 0) return;

  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle* wq = &ep[epIndex].wqHandle;
  core::CompletionQueueHandle* cq = &ep[epIndex].cqHandle;
  uint32_t qpn = ep[epIndex].qpn;

  // Determine if chunking is needed (VMM heap mode)
  bool needsChunking = globalGpuStates->useVMMHeap;

  // Query signal destination address once (signal always goes to same address)
  uintptr_t signalRaddr;
  uint32_t signalRkey;
  QueryRemoteAddr(signalDest, pe, signalRaddr, signalRkey);

  uintptr_t srcStartAddr = reinterpret_cast<uintptr_t>(source);
  uintptr_t dstStartAddr = reinterpret_cast<uintptr_t>(dest);
  size_t remaining = bytes;
  size_t currentOffset = 0;

  // Main loop: process one chunk per iteration
  while (true) {
    // Check if current thread still has data to transfer
    bool has_remaining = (remaining > 0);

    // Synchronize within warp: get mask of threads that still have work
    uint64_t activemask = __ballot(has_remaining);
    if (activemask == 0) {
      break;  // All threads in warp are done
    }

    // Recalculate active lane info for threads with remaining data
    uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
    uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
    bool is_leader{my_logical_lane_id == num_active_lanes - 1};
    const uint64_t leader_phys_lane_id = core::GetLastActiveLaneID(activemask);

    // Inactive threads skip actual work but stay synchronized
    if (!has_remaining) {
      continue;
    }

    // Determine transfer size for this chunk
    uintptr_t srcAddr = srcStartAddr + currentOffset;
    uintptr_t dstAddr = dstStartAddr + currentOffset;
    uint32_t lkey = globalGpuStates->heapObj->lkey;
    uintptr_t raddr;
    uint32_t rkey;
    size_t transfer_size;

    if (!needsChunking) {
      // Static Heap: single transfer, direct rkey access
      transfer_size = remaining;
      size_t offset = dstAddr - globalGpuStates->heapBaseAddr;
      raddr = globalGpuStates->heapObj->peerPtrs[pe] + offset;
      rkey = globalGpuStates->heapObj->peerRkeys[pe];
    } else {
      // VMM Heap: get chunk-specific rkey and size
      size_t src_chunk_size, dst_chunk_size;
      VmmQueryLocalKey(srcAddr, remaining, lkey, src_chunk_size);
      VmmQueryRemoteAddr(dstAddr, pe, remaining, raddr, rkey, dst_chunk_size);
      transfer_size = src_chunk_size < dst_chunk_size ? src_chunk_size : dst_chunk_size;
    }

    // Each thread checks if this is its last chunk
    bool my_is_last_chunk = (transfer_size == remaining);

    // Synchronize: only send signal if ALL active threads are on their last chunk
    // This ensures warp-uniform decision on num_wqes
    uint64_t all_last_mask = __ballot(my_is_last_chunk);
    bool isLastChunk = (all_last_mask == activemask);

    uint32_t warp_sq_counter{0};
    uint32_t warp_msntbl_counter{0}, warp_psn_counter{0}, warp_rsn_counter{0};
    uint32_t my_sq_counter{0}, my_msntbl_counter{0}, my_psn_counter{0}, my_rsn_counter{0};
    uint32_t psnCnt = 0;
    uint32_t warp_total_psn = 0, my_psn_excl = 0, warp_total_rsn = 0, my_rsn_excl = 0;
    // For last chunk: add 1 WQE for signal; for other chunks: just put
    uint32_t num_wqes = isLastChunk ? (onlyOneSignal ? num_active_lanes + 1 : num_active_lanes * 2)
                                    : num_active_lanes;

    if constexpr (PrvdType == core::ProviderType::BNXT) {
      psnCnt = (transfer_size + wq->mtuSize - 1) / wq->mtuSize;
      // Per-lane PSN unit includes this lane's signal WQE when each lane signals.
      uint32_t psnUnit = (isLastChunk && !onlyOneSignal) ? (psnCnt + 1) : psnCnt;
      my_psn_excl = WarpActivePsnPrefix(psnUnit, activemask, &warp_total_psn);
      if (isLastChunk && onlyOneSignal) warp_total_psn += 1;
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      psnCnt = (transfer_size + wq->mtuSize - 1) / wq->mtuSize;
      // Per-lane PSN unit includes this lane's signal WQE when each lane signals.
      uint32_t psnUnit = (isLastChunk && !onlyOneSignal) ? (psnCnt + 1) : psnCnt;
      my_psn_excl = WarpActivePsnPrefix(psnUnit, activemask, &warp_total_psn);
      if (isLastChunk && onlyOneSignal) {
        warp_total_psn += 1;
        if (signalOp == core::atomicType::AMO_ADD || signalOp == core::atomicType::AMO_SIGNAL_ADD) {
          warp_total_rsn = 1;
        }
      }
      if (isLastChunk && !onlyOneSignal &&
          (signalOp == core::atomicType::AMO_ADD || signalOp == core::atomicType::AMO_SIGNAL_ADD)) {
        warp_total_rsn = num_active_lanes;
        my_rsn_excl = core::GetActiveLaneNum(activemask);
      }
    }
    if (is_leader) {
      if constexpr (PrvdType == core::ProviderType::MLX5) {
        warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_wqes, __ATOMIC_RELAXED,
                                                 __HIP_MEMORY_SCOPE_AGENT);
      } else if constexpr (PrvdType == core::ProviderType::JMRD) {
        warp_sq_counter = core::atomic_add_packed_rsn_and_psn(
            *wq, num_wqes, warp_total_psn, warp_total_rsn, &warp_psn_counter, &warp_rsn_counter);
      } else if constexpr (PrvdType == core::ProviderType::BNXT) {
        core::atomic_add_packed_msn_and_psn(&wq->msnPack, num_wqes, warp_total_psn,
                                            &warp_msntbl_counter, &warp_psn_counter);
        warp_sq_counter = warp_msntbl_counter;
        core::AtomicMaxSerial(&wq->postIdx, warp_sq_counter + num_wqes);
      } else if constexpr (PrvdType == core::ProviderType::PSD) {
        warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_wqes, __ATOMIC_RELAXED,
                                                 __HIP_MEMORY_SCOPE_AGENT);
      } else {
        static_assert(false);
      }
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    if constexpr (PrvdType == core::ProviderType::MLX5) {
      my_sq_counter = warp_sq_counter +
                      (isLastChunk && !onlyOneSignal ? my_logical_lane_id * 2 : my_logical_lane_id);
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
      warp_rsn_counter = __shfl(warp_rsn_counter, leader_phys_lane_id);
      my_sq_counter = warp_sq_counter +
                      (isLastChunk && !onlyOneSignal ? my_logical_lane_id * 2 : my_logical_lane_id);
      my_psn_counter = warp_psn_counter + my_psn_excl;
      my_rsn_counter = warp_rsn_counter + my_rsn_excl;
    } else if constexpr (PrvdType == core::ProviderType::BNXT) {
      warp_msntbl_counter = __shfl(warp_msntbl_counter, leader_phys_lane_id);
      warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
      my_sq_counter = warp_sq_counter +
                      (isLastChunk && !onlyOneSignal ? my_logical_lane_id * 2 : my_logical_lane_id);
      my_msntbl_counter =
          warp_msntbl_counter +
          (isLastChunk && !onlyOneSignal ? my_logical_lane_id * 2 : my_logical_lane_id);
      my_psn_counter = warp_psn_counter + my_psn_excl;
    } else if constexpr (PrvdType == core::ProviderType::PSD) {
      my_sq_counter = warp_sq_counter +
                      (isLastChunk && !onlyOneSignal ? my_logical_lane_id * 2 : my_logical_lane_id);
    } else {
      static_assert(false);
    }

    while (true) {
      uint32_t db_touched =
          __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t db_done =
          __hip_atomic_load(&wq->doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t num_active_sq_entries = db_touched - db_done;
      uint32_t num_free_entries = wq->sqWqeNum - num_active_sq_entries;
      uint32_t num_entries_until_warp_last_entry = warp_sq_counter + num_wqes - db_touched;
      if (num_free_entries > num_entries_until_warp_last_entry) {
        break;
      }
      ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
    }

    // Post RDMA write for this chunk
    uint64_t dbr_val;
    if constexpr (PrvdType == core::ProviderType::MLX5) {
      dbr_val = core::PostWrite<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, false,
                                          qpn, srcAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      dbr_val = core::PostWrite<PrvdType>(*wq, my_sq_counter, 0, my_psn_counter, false, qpn,
                                          srcAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::BNXT) {
      dbr_val = core::PostWrite<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter,
                                          false, qpn, srcAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::PSD) {
      dbr_val = core::PostWrite<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, false,
                                          qpn, srcAddr, lkey, raddr, rkey, transfer_size);
    } else {
      static_assert(false);
    }

    // Post signal only for the last chunk
    if (isLastChunk) {
      if (signalOp == core::atomicType::AMO_SET || signalOp == core::atomicType::AMO_SIGNAL_SET) {
        // TODO: not support masked atomic yet, use write inline for now
        bool should_signal = onlyOneSignal ? is_leader : true;
        if (should_signal) {
          if constexpr (PrvdType == core::ProviderType::MLX5) {
            dbr_val = core::PostWriteInline<PrvdType>(
                *wq, my_sq_counter + 1, my_sq_counter + 1, my_sq_counter + 1, is_leader, qpn,
                &signalValue, signalRaddr, signalRkey, sizeof(signalValue));
          } else if constexpr (PrvdType == core::ProviderType::JMRD) {
            dbr_val = core::PostWriteInline<PrvdType>(
                *wq, my_sq_counter + 1, 0, my_psn_counter + psnCnt, is_leader, qpn, &signalValue,
                signalRaddr, signalRkey, sizeof(signalValue));
          } else if constexpr (PrvdType == core::ProviderType::BNXT) {
            dbr_val = core::PostWriteInline<PrvdType>(
                *wq, my_sq_counter + 1, my_msntbl_counter + 1, my_psn_counter + psnCnt, is_leader,
                qpn, &signalValue, signalRaddr, signalRkey, sizeof(signalValue));
          } else if constexpr (PrvdType == core::ProviderType::PSD) {
            dbr_val = core::PostWriteInline<PrvdType>(
                *wq, my_sq_counter + 1, my_sq_counter + 1, my_sq_counter + 1, is_leader, qpn,
                &signalValue, signalRaddr, signalRkey, sizeof(signalValue));
          }
        }
      } else if (signalOp == core::atomicType::AMO_ADD ||
                 signalOp == core::atomicType::AMO_SIGNAL_ADD) {
        core::IbufHandle* ibuf = &ep[epIndex].atomicIbuf;
        bool should_signal = onlyOneSignal ? is_leader : true;
        if (should_signal) {
          if constexpr (PrvdType == core::ProviderType::MLX5) {
            dbr_val = core::PostAtomic<PrvdType>(
                *wq, my_sq_counter + 1, my_sq_counter + 1, my_sq_counter + 1, is_leader, qpn,
                ibuf->addr, ibuf->lkey, signalRaddr, signalRkey, &signalValue, &signalValue,
                sizeof(signalValue), core::atomicType::AMO_ADD);
          } else if constexpr (PrvdType == core::ProviderType::JMRD) {
            dbr_val = core::PostAtomic<PrvdType>(
                *wq, my_sq_counter + 1, my_rsn_counter, my_psn_counter + psnCnt, is_leader, qpn,
                ibuf->addr, ibuf->lkey, signalRaddr, signalRkey, &signalValue, &signalValue,
                sizeof(signalValue), core::atomicType::AMO_ADD);
          } else if constexpr (PrvdType == core::ProviderType::BNXT) {
            dbr_val = core::PostAtomic<PrvdType>(
                *wq, my_sq_counter + 1, my_msntbl_counter + 1, my_psn_counter + psnCnt, is_leader,
                qpn, ibuf->addr, ibuf->lkey, signalRaddr, signalRkey, &signalValue, &signalValue,
                sizeof(signalValue), core::atomicType::AMO_ADD);
          } else if constexpr (PrvdType == core::ProviderType::PSD) {
            dbr_val = core::PostAtomic<PrvdType>(
                *wq, my_sq_counter + 1, my_sq_counter + 1, my_sq_counter + 1, is_leader, qpn,
                ibuf->addr, ibuf->lkey, signalRaddr, signalRkey, &signalValue, &signalValue,
                sizeof(signalValue), core::atomicType::AMO_ADD);
          }
        }
      } else {
        assert(false && "signal unsupported atomic type");
      }
    }

    __threadfence_system();
    if (is_leader) {
      uint64_t db_touched{0};
      do {
        db_touched = __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      } while (db_touched != warp_sq_counter);

      core::UpdateSendDbrRecord<PrvdType>(wq->dbrRecAddr, warp_sq_counter + num_wqes);
      __threadfence_system();
      core::RingDoorbell<PrvdType>(wq->dbrAddr, dbr_val);
      __threadfence_system();

      if (isLastChunk)
        __hip_atomic_fetch_add(&cq->needConsIdx, 1, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      __hip_atomic_store(&wq->dbTouchIdx, warp_sq_counter + num_wqes, __ATOMIC_RELAXED,
                         __HIP_MEMORY_SCOPE_AGENT);
    }
    __threadfence_system();

    // Move to next chunk
    currentOffset += transfer_size;
    remaining -= transfer_size;
  }
}

template <>
inline __device__ void ShmemPutMemNbiSignalThreadKernel<application::TransportType::RDMA, true>(
    const void* dest, const void* source, size_t bytes, const void* signalDest,
    uint64_t signalValue, core::atomicType signalOp, int pe, int qpId) {
  bool need_turn{true};
  uint64_t turns = __ballot(need_turn);
  while (turns) {
    uint8_t lane = __ffsll((unsigned long long)turns) - 1;
    int pe_turn = __shfl(pe, lane);
    if (pe_turn == pe) {
      DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(ShmemPutMemNbiSignalThreadKernelAddrImpl, true,
                                                    dest, source, bytes, signalDest, signalValue,
                                                    signalOp, pe, qpId);
      need_turn = false;
    }
    turns = __ballot(need_turn);
  }
}

template <>
inline __device__ void ShmemPutMemNbiSignalThreadKernel<application::TransportType::RDMA, false>(
    const void* dest, const void* source, size_t bytes, const void* signalDest,
    uint64_t signalValue, core::atomicType signalOp, int pe, int qpId) {
  bool need_turn{true};
  uint64_t turns = __ballot(need_turn);
  while (turns) {
    uint8_t lane = __ffsll((unsigned long long)turns) - 1;
    int pe_turn = __shfl(pe, lane);
    if (pe_turn == pe) {
      DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(ShmemPutMemNbiSignalThreadKernelAddrImpl, false,
                                                    dest, source, bytes, signalDest, signalValue,
                                                    signalOp, pe, qpId);
      need_turn = false;
    }
    turns = __ballot(need_turn);
  }
}

template <core::ProviderType PrvdType, bool onlyOneSignal = true>
inline __device__ void ShmemPutMemNbiSignalWarpKernelAddrImpl(const void* dest, const void* source,
                                                              size_t bytes, const void* signalDest,
                                                              uint64_t signalValue,
                                                              core::atomicType signalOp, int pe,
                                                              int qpId) {
  int laneId = threadIdx.x & (warpSize - 1);
  if (laneId == 0) {
    ShmemPutMemNbiSignalThreadKernelAddrImpl<PrvdType, onlyOneSignal>(
        dest, source, bytes, signalDest, signalValue, signalOp, pe, qpId);
  }
}

template <core::ProviderType PrvdType, bool onlyOneSignal = true>
inline __device__ void ShmemPutMemNbiSignalBlockKernelAddrImpl(const void* dest, const void* source,
                                                               size_t bytes, const void* signalDest,
                                                               uint64_t signalValue,
                                                               core::atomicType signalOp, int pe,
                                                               int qpId) {
  if (core::FlatBlockThreadId() == 0) {
    ShmemPutMemNbiSignalThreadKernelAddrImpl<PrvdType, onlyOneSignal>(
        dest, source, bytes, signalDest, signalValue, signalOp, pe, qpId);
  }
}

template <>
inline __device__ void ShmemPutMemNbiSignalWarpKernel<application::TransportType::RDMA, true>(
    const void* dest, const void* source, size_t bytes, const void* signalDest,
    uint64_t signalValue, core::atomicType signalOp, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(ShmemPutMemNbiSignalWarpKernelAddrImpl, true, dest,
                                                source, bytes, signalDest, signalValue, signalOp,
                                                pe, qpId);
}

template <>
inline __device__ void ShmemPutMemNbiSignalWarpKernel<application::TransportType::RDMA, false>(
    const void* dest, const void* source, size_t bytes, const void* signalDest,
    uint64_t signalValue, core::atomicType signalOp, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(ShmemPutMemNbiSignalWarpKernelAddrImpl, false, dest,
                                                source, bytes, signalDest, signalValue, signalOp,
                                                pe, qpId);
}

template <>
inline __device__ void ShmemPutMemNbiSignalBlockKernel<application::TransportType::RDMA, true>(
    const void* dest, const void* source, size_t bytes, const void* signalDest,
    uint64_t signalValue, core::atomicType signalOp, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(ShmemPutMemNbiSignalBlockKernelAddrImpl, true, dest,
                                                source, bytes, signalDest, signalValue, signalOp,
                                                pe, qpId);
}

template <>
inline __device__ void ShmemPutMemNbiSignalBlockKernel<application::TransportType::RDMA, false>(
    const void* dest, const void* source, size_t bytes, const void* signalDest,
    uint64_t signalValue, core::atomicType signalOp, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_BOOL(ShmemPutMemNbiSignalBlockKernelAddrImpl, false,
                                                dest, source, bytes, signalDest, signalValue,
                                                signalOp, pe, qpId);
}

// New pure address-based Atomic operations for RDMA
template <core::ProviderType PrvdType>
inline __device__ void ShmemAtomicSizeNonFetchThreadKernelAddrImpl(const void* dest, void* val,
                                                                   size_t bytes,
                                                                   core::atomicType amoType, int pe,
                                                                   int qpId) {
  if (bytes == 0) return;

  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle* wq = &ep[epIndex].wqHandle;
  core::CompletionQueueHandle* cq = &ep[epIndex].cqHandle;
  uint32_t qpn = ep[epIndex].qpn;
  core::IbufHandle* ibuf = &ep[epIndex].atomicIbuf;

  // Convert addresses to remote addresses (supports both Static Heap and VMM Heap)
  uintptr_t raddr;
  uint32_t rkey;
  QueryRemoteAddr(dest, pe, raddr, rkey);
  uintptr_t laddr = ibuf->addr;
  uint32_t lkey = ibuf->lkey;

  uint64_t activemask = core::GetActiveLaneMask();
  uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
  uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
  bool is_leader{my_logical_lane_id == num_active_lanes - 1};
  const uint64_t leader_phys_lane_id = core::GetLastActiveLaneID(activemask);

  uint32_t warp_sq_counter = 0;
  uint32_t warp_msntbl_counter = 0, warp_psn_counter = 0, warp_rsn_counter = 0;
  uint32_t my_sq_counter = 0, my_msntbl_counter = 0, my_psn_counter = 0, my_rsn_counter = 0;

  if constexpr (PrvdType == core::ProviderType::MLX5) {
    if (is_leader) {
      warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                               __HIP_MEMORY_SCOPE_AGENT);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    if (is_leader) {
      warp_sq_counter = core::atomic_add_packed_rsn_and_psn(*wq, num_active_lanes, num_active_lanes,
                                                            num_active_lanes, &warp_psn_counter,
                                                            &warp_rsn_counter);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
    warp_rsn_counter = __shfl(warp_rsn_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
    my_psn_counter = warp_psn_counter + my_logical_lane_id;
    my_rsn_counter = warp_rsn_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::BNXT) {
    if (is_leader) {
      core::atomic_add_packed_msn_and_psn(&wq->msnPack, num_active_lanes, num_active_lanes,
                                          &warp_msntbl_counter, &warp_psn_counter);
      warp_sq_counter = warp_msntbl_counter;
      core::AtomicMaxSerial(&wq->postIdx, warp_sq_counter + num_active_lanes);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    warp_msntbl_counter = __shfl(warp_msntbl_counter, leader_phys_lane_id);
    warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
    my_msntbl_counter = warp_msntbl_counter + my_logical_lane_id;
    my_psn_counter = warp_psn_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    if (is_leader) {
      warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                               __HIP_MEMORY_SCOPE_AGENT);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
  } else {
    static_assert(false);
  }

  while (true) {
    uint32_t db_touched =
        __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t db_done = __hip_atomic_load(&wq->doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t num_active_sq_entries = db_touched - db_done;
    uint32_t num_free_entries = wq->sqWqeNum - num_active_sq_entries;
    uint32_t num_entries_until_warp_last_entry = warp_sq_counter + num_active_lanes - db_touched;
    if (num_free_entries > num_entries_until_warp_last_entry) break;
    ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
  }

  uint64_t dbr_val;
  if constexpr (PrvdType == core::ProviderType::MLX5) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader, qpn,
                                   laddr, lkey, raddr, rkey, val, val, bytes, amoType);
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_rsn_counter, my_psn_counter, is_leader,
                                   qpn, laddr, lkey, raddr, rkey, val, val, bytes, amoType);
  } else if constexpr (PrvdType == core::ProviderType::BNXT) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter, is_leader,
                                   qpn, laddr, lkey, raddr, rkey, val, val, bytes, amoType);
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader, qpn,
                                   laddr, lkey, raddr, rkey, val, val, bytes, amoType);
  }

  __threadfence_system();
  if (is_leader) {
    uint64_t db_touched = 0;
    do {
      db_touched = __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    } while (db_touched != warp_sq_counter);

    core::UpdateSendDbrRecord<PrvdType>(wq->dbrRecAddr, warp_sq_counter + num_active_lanes);
    __threadfence_system();
    core::RingDoorbell<PrvdType>(wq->dbrAddr, dbr_val);

    __hip_atomic_fetch_add(&cq->needConsIdx, 1, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    __hip_atomic_store(&wq->dbTouchIdx, warp_sq_counter + num_active_lanes, __ATOMIC_RELAXED,
                       __HIP_MEMORY_SCOPE_AGENT);
  }
}

template <>
inline __device__ void ShmemAtomicSizeNonFetchThreadKernel<application::TransportType::RDMA>(
    const void* dest, void* val, size_t bytes, core::atomicType amoType, int pe, int qpId) {
  bool need_turn{true};
  uint64_t turns = __ballot(need_turn);
  while (turns) {
    uint8_t lane = __ffsll((unsigned long long)turns) - 1;
    int pe_turn = __shfl(pe, lane);
    if (pe_turn == pe) {
      DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemAtomicSizeNonFetchThreadKernelAddrImpl, dest, val,
                                          bytes, amoType, pe, qpId);
      need_turn = false;
    }
    turns = __ballot(need_turn);
  }
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemAtomicSizeNonFetchWarpKernelAddrImpl(const void* dest, void* val,
                                                                 size_t bytes,
                                                                 core::atomicType amoType, int pe,
                                                                 int qpId) {
  int laneId = threadIdx.x & (warpSize - 1);
  if (laneId == 0) {
    ShmemAtomicSizeNonFetchThreadKernelAddrImpl<PrvdType>(dest, val, bytes, amoType, pe, qpId);
  }
}

template <>
inline __device__ void ShmemAtomicSizeNonFetchWarpKernel<application::TransportType::RDMA>(
    const void* dest, void* val, size_t bytes, core::atomicType amoType, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemAtomicSizeNonFetchWarpKernelAddrImpl, dest, val, bytes,
                                      amoType, pe, qpId);
}

// New pure address-based Atomic Fetch operations for RDMA
template <core::ProviderType PrvdType, typename T>
inline __device__ T ShmemAtomicTypeFetchThreadKernelAddrImpl(const void* dest, void* val,
                                                             void* compare, size_t bytes,
                                                             core::atomicType amoType, int pe,
                                                             int qpId) {
  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle* wq = &ep[epIndex].wqHandle;
  core::CompletionQueueHandle* cq = &ep[epIndex].cqHandle;
  uint32_t qpn = ep[epIndex].qpn;
  core::IbufHandle* ibuf = &ep[epIndex].atomicIbuf;

  uint64_t activemask = core::GetActiveLaneMask();
  uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
  uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
  bool is_leader = (my_logical_lane_id == num_active_lanes - 1);
  uint64_t leader_phys_lane_id = core::GetLastActiveLaneID(activemask);

  uint32_t base_slot = 0;
  if (is_leader) {
    base_slot = ShmemGetAtomicIbufSlot(ibuf, num_active_lanes);
  }
  uint32_t my_slot = __shfl(base_slot, leader_phys_lane_id) + my_logical_lane_id;
  uint32_t my_slot_index = my_slot & (ibuf->nslots - 1);
  uintptr_t laddr = ibuf->addr + (my_slot_index + 1) * application::ATOMIC_IBUF_SLOT_SIZE;
  uint32_t lkey = ibuf->lkey;

  // Convert addresses to remote addresses (supports both Static Heap and VMM Heap)
  uintptr_t raddr;
  uint32_t rkey;
  QueryRemoteAddr(dest, pe, raddr, rkey);

  uint32_t warp_sq_counter = 0;
  uint32_t warp_msntbl_counter = 0, warp_psn_counter = 0, warp_rsn_counter = 0;
  uint32_t my_sq_counter = 0, my_msntbl_counter = 0, my_psn_counter = 0, my_rsn_counter = 0;

  if constexpr (PrvdType == core::ProviderType::MLX5) {
    if (is_leader) {
      warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                               __HIP_MEMORY_SCOPE_AGENT);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    if (is_leader) {
      warp_sq_counter = core::atomic_add_packed_rsn_and_psn(*wq, num_active_lanes, num_active_lanes,
                                                            num_active_lanes, &warp_psn_counter,
                                                            &warp_rsn_counter);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
    warp_rsn_counter = __shfl(warp_rsn_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
    my_psn_counter = warp_psn_counter + my_logical_lane_id;
    my_rsn_counter = warp_rsn_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::BNXT) {
    if (is_leader) {
      core::atomic_add_packed_msn_and_psn(&wq->msnPack, num_active_lanes, num_active_lanes,
                                          &warp_msntbl_counter, &warp_psn_counter);
      warp_sq_counter = warp_msntbl_counter;
      core::AtomicMaxSerial(&wq->postIdx, warp_sq_counter + num_active_lanes);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    warp_msntbl_counter = __shfl(warp_msntbl_counter, leader_phys_lane_id);
    warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
    my_msntbl_counter = warp_msntbl_counter + my_logical_lane_id;
    my_psn_counter = warp_psn_counter + my_logical_lane_id;
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    if (is_leader) {
      warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                               __HIP_MEMORY_SCOPE_AGENT);
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    my_sq_counter = warp_sq_counter + my_logical_lane_id;
  } else {
    static_assert(false);
  }

  while (true) {
    uint32_t db_touched =
        __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t db_done = __hip_atomic_load(&wq->doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    uint32_t num_active_sq_entries = db_touched - db_done;
    uint32_t num_free_entries = wq->sqWqeNum - num_active_sq_entries;
    uint32_t num_entries_until_warp_last_entry = warp_sq_counter + num_active_lanes - db_touched;
    if (num_free_entries > num_entries_until_warp_last_entry) break;
    ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
  }

  uint64_t dbr_val;
  if constexpr (PrvdType == core::ProviderType::MLX5) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader, qpn,
                                   laddr, lkey, raddr, rkey, val, compare, bytes, amoType);
  } else if constexpr (PrvdType == core::ProviderType::JMRD) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_rsn_counter, my_psn_counter, is_leader,
                                   qpn, laddr, lkey, raddr, rkey, val, compare, bytes, amoType);
  } else if constexpr (PrvdType == core::ProviderType::BNXT) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter, is_leader,
                                   qpn, laddr, lkey, raddr, rkey, val, compare, bytes, amoType);
  } else if constexpr (PrvdType == core::ProviderType::PSD) {
    dbr_val =
        core::PostAtomic<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader, qpn,
                                   laddr, lkey, raddr, rkey, val, compare, bytes, amoType);
  }

  __threadfence_system();
  if (is_leader) {
    uint64_t db_touched = 0;
    do {
      db_touched = __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    } while (db_touched != warp_sq_counter);

    core::UpdateSendDbrRecord<PrvdType>(wq->dbrRecAddr, warp_sq_counter + num_active_lanes);
    __threadfence_system();
    core::RingDoorbell<PrvdType>(wq->dbrAddr, dbr_val);

    __hip_atomic_fetch_add(&cq->needConsIdx, 1, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
    __hip_atomic_store(&wq->dbTouchIdx, warp_sq_counter + num_active_lanes, __ATOMIC_RELAXED,
                       __HIP_MEMORY_SCOPE_AGENT);
  }

  ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
  T ret = *reinterpret_cast<volatile T*>(laddr);
  if (sizeof(T) == 4) ret = BSWAP32((uint32_t)ret);

  if (is_leader) {
    ShmemReleaseAtomicIbufSlot(ibuf, base_slot, num_active_lanes);
  }

  return ret;
}

#define DEFINE_SHMEM_ATOMIC_TYPE_FETCH_THREAD_KERNEL_RDMA_ADDR(TypeName, T)                       \
  template <>                                                                                     \
  inline __device__ T ShmemAtomicTypeFetchThreadKernel<application::TransportType::RDMA, T>(      \
      const void* dest, void* val, void* compare, size_t bytes, core::atomicType amoType, int pe, \
      int qpId) {                                                                                 \
    bool need_turn{true};                                                                         \
    uint64_t turns = __ballot(need_turn);                                                         \
    T result{};                                                                                   \
    while (turns) {                                                                               \
      uint8_t lane = __ffsll((unsigned long long)turns) - 1;                                      \
      int pe_turn = __shfl(pe, lane);                                                             \
      if (pe_turn == pe) {                                                                        \
        result = DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_RETURN(                                 \
            ShmemAtomicTypeFetchThreadKernelAddrImpl, T, dest, val, compare, bytes, amoType, pe,  \
            qpId);                                                                                \
        need_turn = false;                                                                        \
      }                                                                                           \
      turns = __ballot(need_turn);                                                                \
    }                                                                                             \
    return result;                                                                                \
  }

DEFINE_SHMEM_ATOMIC_TYPE_FETCH_THREAD_KERNEL_RDMA_ADDR(Uint32, uint32_t)
DEFINE_SHMEM_ATOMIC_TYPE_FETCH_THREAD_KERNEL_RDMA_ADDR(Uint64, uint64_t)
DEFINE_SHMEM_ATOMIC_TYPE_FETCH_THREAD_KERNEL_RDMA_ADDR(Int32, int32_t)
DEFINE_SHMEM_ATOMIC_TYPE_FETCH_THREAD_KERNEL_RDMA_ADDR(Int64, int64_t)

template <core::ProviderType PrvdType, typename T>
inline __device__ T ShmemAtomicTypeFetchWarpKernelAddrImpl(const void* dest, void* val,
                                                           void* compare, size_t bytes,
                                                           core::atomicType amoType, int pe,
                                                           int qpId) {
  int laneId = threadIdx.x & (warpSize - 1);
  if (laneId == 0) {
    return ShmemAtomicTypeFetchThreadKernelAddrImpl<PrvdType, T>(dest, val, compare, bytes, amoType,
                                                                 pe, qpId);
  }
  return T{};
}

#define DEFINE_SHMEM_ATOMIC_TYPE_FETCH_WARP_KERNEL_RDMA_ADDR(TypeName, T)                         \
  template <>                                                                                     \
  inline __device__ T ShmemAtomicTypeFetchWarpKernel<application::TransportType::RDMA, T>(        \
      const void* dest, void* val, void* compare, size_t bytes, core::atomicType amoType, int pe, \
      int qpId) {                                                                                 \
    return DISPATCH_PROVIDER_TYPE_COMPILE_TIME_WITH_RETURN(                                       \
        ShmemAtomicTypeFetchWarpKernelAddrImpl, T, dest, val, compare, bytes, amoType, pe, qpId); \
  }

DEFINE_SHMEM_ATOMIC_TYPE_FETCH_WARP_KERNEL_RDMA_ADDR(Uint32, uint32_t)
DEFINE_SHMEM_ATOMIC_TYPE_FETCH_WARP_KERNEL_RDMA_ADDR(Uint64, uint64_t)
DEFINE_SHMEM_ATOMIC_TYPE_FETCH_WARP_KERNEL_RDMA_ADDR(Int32, int32_t)
DEFINE_SHMEM_ATOMIC_TYPE_FETCH_WARP_KERNEL_RDMA_ADDR(Int64, int64_t)

/* ---------------------------------------------------------------------------------------------- */
/*                                    GetMemNbi (SymmMemObjPtr)                                   */
/* ---------------------------------------------------------------------------------------------- */
template <core::ProviderType PrvdType>
inline __device__ void ShmemGetMemNbiThreadKernelImpl(const application::SymmMemObjPtr dest,
                                                      size_t destOffset,
                                                      const application::SymmMemObjPtr source,
                                                      size_t sourceOffset, size_t bytes, int pe,
                                                      int qpId) {
  if (bytes == 0) return;

  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle* wq = &ep[epIndex].wqHandle;
  core::CompletionQueueHandle* cq = &ep[epIndex].cqHandle;
  uint32_t qpn = ep[epIndex].qpn;

  bool needsChunking = globalGpuStates->useVMMHeap;
  size_t currentOffset = 0;
  size_t remaining = bytes;

  while (true) {
    bool has_remaining = (remaining > 0);

    uint64_t activemask = __ballot(has_remaining);
    if (activemask == 0) {
      break;
    }

    uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
    uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
    bool is_leader{my_logical_lane_id == num_active_lanes - 1};
    const uint64_t leader_phys_lane_id = core::GetLastActiveLaneID(activemask);

    if (!has_remaining) {
      continue;
    }

    uint32_t lkey, rkey;
    uintptr_t destAddr, raddr;
    size_t transfer_size;

    if (!needsChunking) {
      lkey = dest->lkey;
      destAddr = reinterpret_cast<uintptr_t>(dest->localPtr) + destOffset + currentOffset;
      raddr = source->peerPtrs[pe] + sourceOffset + currentOffset;
      rkey = source->peerRkeys[pe];
      transfer_size = remaining;
    } else {
      destAddr = reinterpret_cast<uintptr_t>(dest->localPtr) + destOffset + currentOffset;
      size_t dst_chunk_size;
      VmmQueryLocalKey(destAddr, remaining, lkey, dst_chunk_size);

      uintptr_t srcAddr =
          reinterpret_cast<uintptr_t>(source->localPtr) + sourceOffset + currentOffset;
      size_t src_chunk_size;
      VmmQueryRemoteAddr(srcAddr, pe, remaining, raddr, rkey, src_chunk_size);

      transfer_size = dst_chunk_size < src_chunk_size ? dst_chunk_size : src_chunk_size;
    }

    uint32_t warp_sq_counter{0};
    uint32_t warp_msntbl_counter{0}, warp_psn_counter{0}, warp_rsn_counter{0};
    uint32_t my_sq_counter{0}, my_msntbl_counter{0}, my_psn_counter{0}, my_rsn_counter{0};
    uint32_t psnCnt = 0;
    uint32_t warp_total_psn = 0, my_psn_excl = 0, warp_total_rsn = 0, my_rsn_excl = 0;

    if constexpr (PrvdType == core::ProviderType::BNXT) {
      psnCnt = (transfer_size + wq->mtuSize - 1) / wq->mtuSize;
      my_psn_excl = WarpActivePsnPrefix(psnCnt, activemask, &warp_total_psn);
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      psnCnt = (transfer_size + wq->mtuSize - 1) / wq->mtuSize;
      my_psn_excl = WarpActivePsnPrefix(psnCnt, activemask, &warp_total_psn);
      warp_total_rsn = num_active_lanes;
      my_rsn_excl = my_logical_lane_id;
    }
    if (is_leader) {
      if constexpr (PrvdType == core::ProviderType::MLX5) {
        warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                                 __HIP_MEMORY_SCOPE_AGENT);
      } else if constexpr (PrvdType == core::ProviderType::JMRD) {
        warp_sq_counter = core::atomic_add_packed_rsn_and_psn(*wq, num_active_lanes, warp_total_psn,
                                                              warp_total_rsn, &warp_psn_counter,
                                                              &warp_rsn_counter);
      } else if constexpr (PrvdType == core::ProviderType::BNXT) {
        core::atomic_add_packed_msn_and_psn(&wq->msnPack, num_active_lanes, warp_total_psn,
                                            &warp_msntbl_counter, &warp_psn_counter);
        warp_sq_counter = warp_msntbl_counter;
        core::AtomicMaxSerial(&wq->postIdx, warp_sq_counter + num_active_lanes);
      } else if constexpr (PrvdType == core::ProviderType::PSD) {
        warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                                 __HIP_MEMORY_SCOPE_AGENT);
      } else {
        static_assert(false);
      }
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    if constexpr (PrvdType == core::ProviderType::MLX5) {
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
      warp_rsn_counter = __shfl(warp_rsn_counter, leader_phys_lane_id);
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
      my_psn_counter = warp_psn_counter + my_psn_excl;
      my_rsn_counter = warp_rsn_counter + my_rsn_excl;
    } else if constexpr (PrvdType == core::ProviderType::BNXT) {
      warp_msntbl_counter = __shfl(warp_msntbl_counter, leader_phys_lane_id);
      warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
      my_msntbl_counter = warp_msntbl_counter + my_logical_lane_id;
      my_psn_counter = warp_psn_counter + my_psn_excl;
    } else if constexpr (PrvdType == core::ProviderType::PSD) {
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
    } else {
      static_assert(false);
    }

    while (true) {
      uint32_t db_touched =
          __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t db_done =
          __hip_atomic_load(&wq->doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t num_active_sq_entries = db_touched - db_done;
      uint32_t num_free_entries = wq->sqWqeNum - num_active_sq_entries;
      uint32_t num_entries_until_warp_last_entry = warp_sq_counter + num_active_lanes - db_touched;
      if (num_free_entries > num_entries_until_warp_last_entry) {
        break;
      }
      ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
    }

    uint64_t dbr_val;
    if constexpr (PrvdType == core::ProviderType::MLX5) {
      dbr_val =
          core::PostRead<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader, qpn,
                                   destAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      dbr_val =
          core::PostRead<PrvdType>(*wq, my_sq_counter, my_rsn_counter, my_psn_counter, is_leader,
                                   qpn, destAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::BNXT) {
      dbr_val =
          core::PostRead<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter, is_leader,
                                   qpn, destAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::PSD) {
      dbr_val =
          core::PostRead<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader, qpn,
                                   destAddr, lkey, raddr, rkey, transfer_size);
    } else {
      static_assert(false);
    }
    __threadfence_system();
    if (is_leader) {
      uint64_t db_touched{0};
      do {
        db_touched = __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      } while (db_touched != warp_sq_counter);

      core::UpdateSendDbrRecord<PrvdType>(wq->dbrRecAddr, warp_sq_counter + num_active_lanes);
      __threadfence_system();
      core::RingDoorbell<PrvdType>(wq->dbrAddr, dbr_val);
      __threadfence_system();

      __hip_atomic_fetch_add(&cq->needConsIdx, 1, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      __hip_atomic_store(&wq->dbTouchIdx, warp_sq_counter + num_active_lanes, __ATOMIC_RELAXED,
                         __HIP_MEMORY_SCOPE_AGENT);
    }
    __threadfence_system();

    currentOffset += transfer_size;
    remaining -= transfer_size;
  }
}

template <>
inline __device__ void ShmemGetMemNbiThreadKernel<application::TransportType::RDMA>(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes, int pe, int qpId) {
  bool need_turn{true};
  uint64_t turns = __ballot(need_turn);
  while (turns) {
    uint8_t lane = __ffsll((unsigned long long)turns) - 1;
    int pe_turn = __shfl(pe, lane);
    if (pe_turn == pe) {
      DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemGetMemNbiThreadKernelImpl, dest, destOffset, source,
                                          sourceOffset, bytes, pe, qpId);
      need_turn = false;
    }
    turns = __ballot(need_turn);
  }
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemGetMemNbiWarpKernelImpl(const application::SymmMemObjPtr dest,
                                                    size_t destOffset,
                                                    const application::SymmMemObjPtr source,
                                                    size_t sourceOffset, size_t bytes, int pe,
                                                    int qpId) {
  int laneId = threadIdx.x & (warpSize - 1);
  if (laneId == 0) {
    ShmemGetMemNbiThreadKernelImpl<PrvdType>(dest, destOffset, source, sourceOffset, bytes, pe,
                                             qpId);
  }
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemGetMemNbiBlockKernelImpl(const application::SymmMemObjPtr dest,
                                                     size_t destOffset,
                                                     const application::SymmMemObjPtr source,
                                                     size_t sourceOffset, size_t bytes, int pe,
                                                     int qpId) {
  int threadId = core::FlatBlockThreadId();
  if (threadId == 0) {
    ShmemGetMemNbiThreadKernelImpl<PrvdType>(dest, destOffset, source, sourceOffset, bytes, pe,
                                             qpId);
  }
}

template <>
inline __device__ void ShmemGetMemNbiWarpKernel<application::TransportType::RDMA>(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemGetMemNbiWarpKernelImpl, dest, destOffset, source,
                                      sourceOffset, bytes, pe, qpId);
}

template <>
inline __device__ void ShmemGetMemNbiBlockKernel<application::TransportType::RDMA>(
    const application::SymmMemObjPtr dest, size_t destOffset,
    const application::SymmMemObjPtr source, size_t sourceOffset, size_t bytes, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemGetMemNbiBlockKernelImpl, dest, destOffset, source,
                                      sourceOffset, bytes, pe, qpId);
}

/* ---------------------------------------------------------------------------------------------- */
/*                               GetMemNbi (Pure Address-Based)                                   */
/* ---------------------------------------------------------------------------------------------- */
template <core::ProviderType PrvdType>
inline __device__ void ShmemGetMemNbiThreadKernelAddrImpl(void* dest, const void* source,
                                                          size_t bytes, int pe, int qpId) {
  if (bytes == 0) return;

  GpuStates* globalGpuStates = GetGlobalGpuStatesPtr();
  ShmemRdmaEndpoint* ep = globalGpuStates->rdmaEndpoints;
  int epIndex = pe * globalGpuStates->numQpPerPe + (qpId % globalGpuStates->numQpPerPe);
  core::WorkQueueHandle* wq = &ep[epIndex].wqHandle;
  core::CompletionQueueHandle* cq = &ep[epIndex].cqHandle;
  uint32_t qpn = ep[epIndex].qpn;

  bool needsChunking = globalGpuStates->useVMMHeap;

  uintptr_t destStartAddr = reinterpret_cast<uintptr_t>(dest);
  uintptr_t srcStartAddr = reinterpret_cast<uintptr_t>(source);
  size_t remaining = bytes;
  size_t currentOffset = 0;

  while (true) {
    bool has_remaining = (remaining > 0);

    uint64_t activemask = __ballot(has_remaining);
    if (activemask == 0) {
      break;
    }

    uint8_t num_active_lanes = core::GetActiveLaneCount(activemask);
    uint8_t my_logical_lane_id = core::GetActiveLaneNum(activemask);
    bool is_leader{my_logical_lane_id == num_active_lanes - 1};
    const uint64_t leader_phys_lane_id = core::GetLastActiveLaneID(activemask);

    if (!has_remaining) {
      continue;
    }

    uintptr_t destAddr = destStartAddr + currentOffset;
    uintptr_t srcAddr = srcStartAddr + currentOffset;
    uint32_t lkey = globalGpuStates->heapObj->lkey;
    uintptr_t raddr;
    uint32_t rkey;
    size_t transfer_size;

    if (!needsChunking) {
      transfer_size = remaining;
      size_t offset = srcAddr - globalGpuStates->heapBaseAddr;
      raddr = globalGpuStates->heapObj->peerPtrs[pe] + offset;
      rkey = globalGpuStates->heapObj->peerRkeys[pe];
    } else {
      size_t dst_chunk_size, src_chunk_size;
      VmmQueryLocalKey(destAddr, remaining, lkey, dst_chunk_size);
      VmmQueryRemoteAddr(srcAddr, pe, remaining, raddr, rkey, src_chunk_size);
      transfer_size = dst_chunk_size < src_chunk_size ? dst_chunk_size : src_chunk_size;
    }

    uint32_t warp_sq_counter{0};
    uint32_t warp_msntbl_counter{0}, warp_psn_counter{0}, warp_rsn_counter{0};
    uint32_t my_sq_counter{0}, my_msntbl_counter{0}, my_psn_counter{0}, my_rsn_counter{0};
    uint32_t psnCnt = 0;
    uint32_t warp_total_psn = 0, my_psn_excl = 0, warp_total_rsn = 0, my_rsn_excl = 0;

    if constexpr (PrvdType == core::ProviderType::BNXT) {
      psnCnt = (transfer_size + wq->mtuSize - 1) / wq->mtuSize;
      my_psn_excl = WarpActivePsnPrefix(psnCnt, activemask, &warp_total_psn);
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      psnCnt = (transfer_size + wq->mtuSize - 1) / wq->mtuSize;
      my_psn_excl = WarpActivePsnPrefix(psnCnt, activemask, &warp_total_psn);
      warp_total_rsn = num_active_lanes;
      my_rsn_excl = my_logical_lane_id;
    }
    if (is_leader) {
      if constexpr (PrvdType == core::ProviderType::MLX5) {
        warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                                 __HIP_MEMORY_SCOPE_AGENT);
      } else if constexpr (PrvdType == core::ProviderType::JMRD) {
        warp_sq_counter = core::atomic_add_packed_rsn_and_psn(*wq, num_active_lanes, warp_total_psn,
                                                              warp_total_rsn, &warp_psn_counter,
                                                              &warp_rsn_counter);
      } else if constexpr (PrvdType == core::ProviderType::BNXT) {
        core::atomic_add_packed_msn_and_psn(&wq->msnPack, num_active_lanes, warp_total_psn,
                                            &warp_msntbl_counter, &warp_psn_counter);
        warp_sq_counter = warp_msntbl_counter;
        core::AtomicMaxSerial(&wq->postIdx, warp_sq_counter + num_active_lanes);
      } else if constexpr (PrvdType == core::ProviderType::PSD) {
        warp_sq_counter = __hip_atomic_fetch_add(&wq->postIdx, num_active_lanes, __ATOMIC_RELAXED,
                                                 __HIP_MEMORY_SCOPE_AGENT);
      } else {
        static_assert(false);
      }
    }
    warp_sq_counter = __shfl(warp_sq_counter, leader_phys_lane_id);
    if constexpr (PrvdType == core::ProviderType::MLX5) {
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
      warp_rsn_counter = __shfl(warp_rsn_counter, leader_phys_lane_id);
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
      my_psn_counter = warp_psn_counter + my_psn_excl;
      my_rsn_counter = warp_rsn_counter + my_rsn_excl;
    } else if constexpr (PrvdType == core::ProviderType::BNXT) {
      warp_msntbl_counter = __shfl(warp_msntbl_counter, leader_phys_lane_id);
      warp_psn_counter = __shfl(warp_psn_counter, leader_phys_lane_id);
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
      my_msntbl_counter = warp_msntbl_counter + my_logical_lane_id;
      my_psn_counter = warp_psn_counter + my_psn_excl;
    } else if constexpr (PrvdType == core::ProviderType::PSD) {
      my_sq_counter = warp_sq_counter + my_logical_lane_id;
    } else {
      static_assert(false);
    }

    while (true) {
      uint32_t db_touched =
          __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t db_done =
          __hip_atomic_load(&wq->doneIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      uint32_t num_active_sq_entries = db_touched - db_done;
      uint32_t num_free_entries = wq->sqWqeNum - num_active_sq_entries;
      uint32_t num_entries_until_warp_last_entry = warp_sq_counter + num_active_lanes - db_touched;
      if (num_free_entries > num_entries_until_warp_last_entry) {
        break;
      }
      ShmemQuietThreadKernelImpl<PrvdType>(pe, qpId);
    }

    uint64_t dbr_val;
    if constexpr (PrvdType == core::ProviderType::MLX5) {
      dbr_val =
          core::PostRead<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader, qpn,
                                   destAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::JMRD) {
      dbr_val =
          core::PostRead<PrvdType>(*wq, my_sq_counter, my_rsn_counter, my_psn_counter, is_leader,
                                   qpn, destAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::BNXT) {
      dbr_val =
          core::PostRead<PrvdType>(*wq, my_sq_counter, my_msntbl_counter, my_psn_counter, is_leader,
                                   qpn, destAddr, lkey, raddr, rkey, transfer_size);
    } else if constexpr (PrvdType == core::ProviderType::PSD) {
      dbr_val =
          core::PostRead<PrvdType>(*wq, my_sq_counter, my_sq_counter, my_sq_counter, is_leader, qpn,
                                   destAddr, lkey, raddr, rkey, transfer_size);
    } else {
      static_assert(false);
    }

    __threadfence_system();
    if (is_leader) {
      uint64_t db_touched{0};
      do {
        db_touched = __hip_atomic_load(&wq->dbTouchIdx, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      } while (db_touched != warp_sq_counter);

      core::UpdateSendDbrRecord<PrvdType>(wq->dbrRecAddr, warp_sq_counter + num_active_lanes);
      __threadfence_system();
      core::RingDoorbell<PrvdType>(wq->dbrAddr, dbr_val);
      __threadfence_system();

      __hip_atomic_fetch_add(&cq->needConsIdx, 1, __ATOMIC_RELAXED, __HIP_MEMORY_SCOPE_AGENT);
      __hip_atomic_store(&wq->dbTouchIdx, warp_sq_counter + num_active_lanes, __ATOMIC_RELAXED,
                         __HIP_MEMORY_SCOPE_AGENT);
    }
    __threadfence_system();

    currentOffset += transfer_size;
    remaining -= transfer_size;
  }
}

template <>
inline __device__ void ShmemGetMemNbiThreadKernel<application::TransportType::RDMA>(
    void* dest, const void* source, size_t bytes, int pe, int qpId) {
  bool need_turn{true};
  uint64_t turns = __ballot(need_turn);
  while (turns) {
    uint8_t lane = __ffsll((unsigned long long)turns) - 1;
    int pe_turn = __shfl(pe, lane);
    if (pe_turn == pe) {
      DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemGetMemNbiThreadKernelAddrImpl, dest, source, bytes,
                                          pe, qpId);
      need_turn = false;
    }
    turns = __ballot(need_turn);
  }
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemGetMemNbiWarpKernelAddrImpl(void* dest, const void* source,
                                                        size_t bytes, int pe, int qpId) {
  int laneId = threadIdx.x & (warpSize - 1);
  if (laneId == 0) {
    ShmemGetMemNbiThreadKernelAddrImpl<PrvdType>(dest, source, bytes, pe, qpId);
  }
}

template <core::ProviderType PrvdType>
inline __device__ void ShmemGetMemNbiBlockKernelAddrImpl(void* dest, const void* source,
                                                         size_t bytes, int pe, int qpId) {
  if (core::FlatBlockThreadId() == 0) {
    ShmemGetMemNbiThreadKernelAddrImpl<PrvdType>(dest, source, bytes, pe, qpId);
  }
}

template <>
inline __device__ void ShmemGetMemNbiWarpKernel<application::TransportType::RDMA>(
    void* dest, const void* source, size_t bytes, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemGetMemNbiWarpKernelAddrImpl, dest, source, bytes, pe,
                                      qpId);
}

template <>
inline __device__ void ShmemGetMemNbiBlockKernel<application::TransportType::RDMA>(
    void* dest, const void* source, size_t bytes, int pe, int qpId) {
  DISPATCH_PROVIDER_TYPE_COMPILE_TIME(ShmemGetMemNbiBlockKernelAddrImpl, dest, source, bytes, pe,
                                      qpId);
}

}  // namespace shmem
}  // namespace mori
